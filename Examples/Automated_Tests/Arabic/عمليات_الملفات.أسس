اشمل "عامة.أسس"؛

عرف ـرئيسي: حزمة
{
  عرف إبدأ: دالة ()
  {
    عرف اسم_الملف: مؤشر[ـمحرف]؛
    اسم_الملف = "/tmp/alususfileoperationtest.txt"؛

    اختبر_خزن_نص(اسم_الملف)؛
    اختبر_جلب_نص(اسم_الملف)؛
    اختبر_خزن_رقميات(اسم_الملف)؛
    اختبر_جلب_رقميات(اسم_الملف)؛
  }؛

  عرف اختبر_خزن_نص: دالة (اسم_الملف: مؤشر[ـمحرف])
  {
    عرف ملف: مؤشر[ـفراغ]؛
    ملف = ـمتم.افتح_ملف(اسم_الملف، "w")؛
    ـمتم.اطبع_إلى_ملف(ملف، "عدد أيام الأسبوع: %d"، 7)؛
    ـمتم.اغلق_ملف(ملف)؛
  }؛

  عرف اختبر_جلب_نص: دالة (اسم_الملف: مؤشر[ـمحرف])
  {
    عرف ملف: مؤشر[ـفراغ]؛
    ملف = ـمتم.افتح_ملف(اسم_الملف، "r")؛
    عرف صوان: مصفوفة[ـمحرف، 100]؛
    ـمتم.تفحص_من_ملف(ملف، "%[^0-9]"، صوان~مؤشر)؛
    ـمتم.اطبع(صوان(0)~مؤشر)؛
    عرف رقم: ـصحيح؛
    ـمتم.تفحص_من_ملف(ملف، "%i"، رقم~مؤشر)؛
    ـمتم.اطبع("%d\ج"، رقم)؛
    ـمتم.اغلق_ملف(ملف)
  }؛

  عرف اختبر_خزن_رقميات: دالة (اسم_الملف: مؤشر[ـمحرف])
  {
    عرف م: مصفوفة[ـصحيح، 8]؛
    عرف ع: ـصحيح؛
    لكل ع = 0، ع < 8، ++ع م(ع) = ع * ع؛
    عرف ملف: مؤشر[ـفراغ]؛
    ملف = ـمتم.افتح_ملف(اسم_الملف، "w")؛
    ـمتم.اكتب_إلى_ملف(م~مؤشر، م(0)~حجم~مثل[ـصحيح]، 8، ملف)؛
    ـمتم.اغلق_ملف(ملف)؛
  }؛

  عرف اختبر_جلب_رقميات: دالة (اسم_الملف: مؤشر[ـمحرف])
  {
    عرف م: مصفوفة[ـصحيح، 8]؛
    عرف ملف: مؤشر[ـفراغ]؛
    ملف = ـمتم.افتح_ملف(اسم_الملف، "r")؛
    ـمتم.اقرأ_من_ملف(م~مؤشر، م(0)~حجم~مثل[ـصحيح]، 8، ملف)؛
    ـمتم.اغلق_ملف(ملف)؛
    عرف ع: ـصحيح؛
    لكل ع = 0، ع < 8، ++ع ـمتم.اطبع("العنصر %d: %d\ج"، ع، م(ع))؛
  }؛
}؛

نفذ ـرئيسي.إبدأ؛

