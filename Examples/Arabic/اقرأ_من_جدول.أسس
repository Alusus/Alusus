اشمل "مـتم/شـبكة.أسس"؛
اشمل "مـتم/طـرفية.أسس"؛
اشمل "مـتم/ذاكـرة.أسس"؛
اشمل "مـتم/نـص.أسس"؛
اشمل "مـتم/مـصفوفة.أسس"؛

@تصدير[atoi] دالة حول_لرقم(مـؤشر_محارف): صحيح؛

استخدم مـتم؛

دالة ابدأ {
  عرف بيانات: مـؤشر_محارف؛
  عرف حجم: صـحيح؛
  عرف الرابط: "https://people.sc.fsu.edu/~jburkardt/data/csv/homes.csv"؛
  إذا شـبكة.هات(الرابط، بيانات~مؤشر، حجم~مؤشر) {
    طـرفية.اطبع("جُلبت البيانات من الشبكة\ج")؛
    عرف نص: نـص = بيانات؛
    ذاكـرة.حرر(بيانات)؛
    عرف عدد: صحيح = جد_عدد_الدور_الأحدث_من(20، نص)؛
    طـرفية.اطبع("عدد الدور الأحدث من عشرين سنة: %d\ج"، عدد)؛
  } وإلا {
    طـرفية.اطبع("فشل جلب المعلومات من الشبكة\ج")؛
  }
}

دالة جد_عدد_الدور_الأحدث_من(عمر: صحيح، جدول: نـص): صحيح {
  عرف أسطر: مـصفوفة[نـص] = جدول.قطع("\ج")؛
  عرف الناتج: صحيح = 0؛
  عرف ع: صحيح؛
  // سنتجاوز الترويسة ونبدأ من السطر الثاني.
  لكل ع = 1، ع < أسطر.هات_الطول()، ++ع {
    إذا أسطر(ع).شذب() == "" أكمل؛
    إذا حول_لرقم(أسطر(ع).قطع(",")(6)) < عمر { ++الناتج }
  }
  أرجع الناتج؛
}

ابدأ()؛

