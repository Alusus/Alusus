اشمل "مـتم/شـبكة.أسس"؛
اشمل "مـتم/طـرفية.أسس"؛
اشمل "مـتم/ذاكـرة.أسس"؛
اشمل "مـتم/نـص.أسس"؛
اشمل "مـتم/مـصفوفة.أسس"؛
اشمل "مـتم/نـظام.أسس"؛

@تصدير[atof] دالة حول_لرقم(مـؤشر_محارف): عائم[64]؛

استخدم مـتم؛

دالة ابدأ {
  عرف بيانات: مـؤشر_محارف؛
  عرف حجم: صـحيح؛
  عرف العدد: صحيح؛
  عرف معدل_الطول: عائم[64]؛
  عرف الرابط: "https://people.sc.fsu.edu/~jburkardt/data/csv/hw_25000.csv"؛
  إذا شـبكة.هات(الرابط، بيانات~مؤشر، حجم~مؤشر) {
    طـرفية.اطبع("جُلبت البيانات من الشبكة\ج")؛
    عرف نص: نـص = بيانات؛
    ذاكـرة.حرر(بيانات)؛
    احسب_معدل_الطول(نص، العدد، معدل_الطول)؛
    طـرفية.اطبع("عدد الأشخاص: %d، معدل الطول: %f\ج"، العدد، معدل_الطول)؛
  } وإلا {
    طـرفية.اطبع("فشل جلب المعلومات من الشبكة.\ج")؛
  }
}

دالة احسب_معدل_الطول(جدول: نـص، العدد: سند[صحيح]، معدل_الطول: سند[عائم[64]]) {
  عرف أسطر: مـصفوفة[نـص] = جدول.قطع("\ج")؛
  إذا أسطر.هات_الطول() < 2 {
      طـرفية.اطبع("بيانات غير صالحة:\ج%s\ج"، جدول.صوان)؛
  }
  العدد = 0؛
  عرف المجموع: عائم[64] = 0؛
  عرف ع: صحيح؛
  // سنتجاوز الترويسة ونبدأ من السطر الثاني.
  لكل ع = 1، ع < أسطر.هات_الطول()، ++ع {
    إذا أسطر(ع).شذب() == "" أكمل؛
    ++العدد؛
    المجموع += حول_لرقم(أسطر(ع).قطع(",")(1))؛
  }
  معدل_الطول = المجموع ÷ العدد؛
}

ابدأ()؛

