اشمل "مـتم/شـبكة.أسس"؛
اشمل "مـتم/طـرفية.أسس"؛
اشمل "مـتم/ذاكـرة.أسس"؛
اشمل "مـتم/نـص.أسس"؛
اشمل "مـتم/مـصفوفة.أسس"؛

@تصدير[atoi] دالة حول_لرقم(مـؤشر_محارف): صحيح؛

استخدم مـتم؛

دالة ابدأ {
  عرف بيانات: مـؤشر_محارف؛
  عرف حجم: صـحيح؛
  عرف الرابط: "http://samplecsvs.s3.amazonaws.com/Sacramentorealestatetransactions.csv"؛
  إذا شـبكة.هات(الرابط، بيانات~مؤشر، حجم~مؤشر) {
    طـرفية.اطبع("جُلبت البيانات من الشبكة\ج")؛
    عرف نص: نـص = بيانات؛
    ذاكـرة.حرر(بيانات)؛
    عرف عدد: صحيح = جد_عدد_الدور_الأكبر_من(3، نص)؛
    طـرفية.اطبع("عدد الدور بأكثر من ثلاث غرف: %d\ج"، عدد)؛
  } وإلا {
    طـرفية.اطبع("فشل جلب المعلومات من الشبكة\ج")؛
  }
}

دالة جد_عدد_الدور_الأكبر_من(عدد_الغرف: صحيح، جدول: نـص): صحيح {
  عرف أسطر: مـصفوفة[نـص] = جدول.قطع("\ر")؛
  عرف الناتج: صحيح = 0؛
  عرف ع: صحيح؛
  // سنتجاوز الترويسة ونبدأ من السطر الثاني.
  لكل ع = 1، ع < أسطر.هات_الطول()، ++ع {
    إذا أسطر(ع).شذب() == "" أكمل؛
    إذا حول_لرقم(أسطر(ع).قطع(",")(4)) > عدد_الغرف { ++الناتج }
  }
  أرجع الناتج؛
}

ابدأ()؛

