اشمل "جـتك/جتك.أسس"؛
اشمل "مـتم/مـحارف.أسس"؛

عرف دلـيل_الودجات: حزمة
{
  عرف النافذة: مؤشر[جـتك.نـافذة_تطبيق]؛

  عرف ابدأ: دالة
  {
    عرف التطبيق: مؤشر[جـتك.تـطبيق]؛
    عرف معرف_التطبيق: مؤشر[محرف]؛
    معرف_التطبيق = "org.alusus.gtk-widget.guide"؛
    التطبيق = جـتك.تـطبيق.أنشئ(معرف_التطبيق، 0)؛
    جـلب.اربط_إشارة(التطبيق، "activate"، فعّل~مؤشر~مثل[مؤشر[دالة]]، 0، 0، 0)؛
    جـتك.تـطبيق.شغل(التطبيق، 0، 0)؛
    جـلب.حرر(التطبيق)؛
  };

  عرف فعّل: دالة (تطبيق: مؤشر[جـتك.تـطبيق]، بيانات: مؤشر)
  {
    // أنشئ النافذة الرئيسية.
    النافذة = جـتك.نـافذة_تطبيق.أنشئ(تطبيق)؛
    جـتك.نـافذة.عين_عنوانا(النافذة، "الأسس: دليل ودجات جتك")؛
    جـتك.نـافذة.عين_الحجم_المبدئي(النافذة، 640، 480)؛

    // أنشئ شريط الحاشية مع قائمة الأوامر وزري حول وخروج.
    جـتك.نـافذة.عين_شريط_عنوان(النافذة، أنشئ_شريط_الحاشية())؛

    // أنشئ جانبان.
    عرف جانبان: مؤشر[جـتك.جـانبان]؛
    جانبان = جـتك.جـانبان.أنشئ(جـتك.اتـجاه._أفقي_)؛
    جـتك.حـاوية.أضف(النافذة، جانبان)؛
    جـتك.جـانبان.عين_الموقع(جانبان، 150)؛

    // أنشئ صندوقا للجانب البعيد.
    عرف صندوق: مؤشر[جـتك.صـندوق]؛
    صندوق = جـتك.صـندوق.أنشئ(جـتك.اتـجاه._عمودي_، 10)؛
    جـتك.جـانبان.احشد2(جانبان، صندوق، 1، 1)؛

    // املأ الجانب البعيد.
    عرف لصيقة: مؤشر[جـتك.لـصيقة]؛
    عرف صوان_نصي: مؤشر[جـتك.صـوان_نصي]؛
    لصيقة = أنشئ_الشعار_واللصيقة(صندوق)؛
    أنشئ_حقلا(صندوق، لصيقة)؛
    صوان_نصي = أنشئ_مساحة_الكتابة(صندوق)؛
    أنشئ_أزرار_مساحة_الكتابة(صندوق، صوان_نصي)؛
    أنشئ_الزر_الثنائي(صندوق، لصيقة)؛
    أنشئ_خانة_التأشير(صندوق، لصيقة)؛

    // املأ الجانب القريب.
    أنشئ_القائمة(جانبان، لصيقة)؛

    جـتك.ودجـة.أظهر_الكل(النافذة)؛
  };

  عرف أنشئ_شريط_الحاشية: دالة () => مؤشر[جـتك.شـريط_الحاشية]
  {
    عرف شريط_الحاشية: مؤشر[جـتك.شـريط_الحاشية]؛
    شريط_الحاشية = جـتك.شـريط_الحاشية.أنشئ()؛
    جـتك.شـريط_الحاشية.عين_عنوانا(شريط_الحاشية، "الأسس: دليل ودجات جتك")؛

    عرف زر_حول: مؤشر[جـتك.الـزر]؛
    زر_حول = جـتك.الـزر.أنشئ_بلصيقة("حول")؛
    جـلب.اربط_إشارة(زر_حول، "clicked"، قد_نقر_زر_حول~مؤشر~مثل[مؤشر[دالة]]، 0، 0، 0)؛
    جـتك.شـريط_الحاشية.احشد_في_المقدمة(شريط_الحاشية، زر_حول)؛

    عرف زر_الخروج: مؤشر[جـتك.الـزر]؛
    زر_الخروج = جـتك.الـزر.أنشئ_بلصيقة("خروج")؛
    جـلب.اربط_إشارة(زر_الخروج، "clicked"، قد_نقر_زر_الخروج~مؤشر~مثل[مؤشر[دالة]]، 0، 0، 0)؛
    جـتك.شـريط_الحاشية.احشد_في_المؤخرة(شريط_الحاشية، زر_الخروج)؛
    
    أنشئ_قائمة_الأوامر(شريط_الحاشية)؛
    
    أرجع شريط_الحاشية؛
  };

  عرف أنشئ_قائمة_الأوامر: دالة (شريط_الحاشية: مؤشر[جـتك.شـريط_الحاشية])
  {
    عرف قائمة_الأوامر: مؤشر[جـتك.قـائمة_أوامر]؛
    قائمة_الأوامر = جـتك.قـائمة_أوامر.أنشئ()؛

    عرف عنصر: مؤشر[جـتك.عـنصر_قائمة_أوامر]؛
    عنصر = جـتك.عـنصر_قائمة_أوامر.أنشئ("حول")؛
    جـلب.اربط_إشارة(عنصر، "activate"، قد_نقر_زر_حول~مؤشر~مثل[مؤشر[دالة]]، 0، 0، 0)؛
    جـتك.صـدفة_قائمة_أوامر.ألحق(قائمة_الأوامر، عنصر)؛
    جـتك.ودجـة.أظهر(عنصر)؛
    عنصر = جـتك.عـنصر_قائمة_أوامر.أنشئ("خروج")؛
    جـلب.اربط_إشارة(عنصر، "activate"، قد_نقر_زر_الخروج~مؤشر~مثل[مؤشر[دالة]]، 0، 0، 0)؛
    جـتك.صـدفة_قائمة_أوامر.ألحق(قائمة_الأوامر، عنصر)؛
    جـتك.ودجـة.أظهر(عنصر)؛
    
    عرف زر: مؤشر[جـتك.مـطلق_القائمة]؛
    زر = جـتك.مـطلق_القائمة.أنشئ()؛
    جـتك.مـطلق_القائمة.عين_منبثق(زر، قائمة_الأوامر)؛
    جـتك.شـريط_الحاشية.احشد_في_المؤخرة(شريط_الحاشية، زر)؛
  };

  عرف أنشئ_القائمة: دالة (جانبان: مؤشر[جـتك.جـانبان]، لصيقة: مؤشر[جـتك.لـصيقة])
  {
    عرف القائمة: مؤشر[جـتك.قـائمة]؛
    القائمة = جـتك.قـائمة.أنشئ()؛
    جـتك.جـانبان.احشد1(جانبان، القائمة، 0، 1)؛
    
    عرف محارف: مصفوفة[محرف، 100]؛
    عرف س: صحيح؛
    لكل س = 1، س <= 10، ++س {
      مـتم.مـحارف.اطبع(محارف~مؤشر، "السطر %d"، س)؛
      عرف لصيقة: مؤشر[جـتك.لـصيقة]؛
      لصيقة = جـتك.لـصيقة.أنشئ(محارف~مؤشر)؛
      جـتك.قـائمة.أولج(القائمة، لصيقة، -1)؛
    }؛
    
    جـلب.اربط_إشارة(القائمة، "row-selected"، قد_تغير_اختيار_القائمة~مؤشر~مثل[مؤشر[دالة]]، لصيقة، 0، 0)؛
  };

  عرف أنشئ_الشعار_واللصيقة: دالة (صندوق: مؤشر[جـتك.صـندوق]) => مؤشر[جـتك.لـصيقة]
  {
    عرف صندوق_أفقي: مؤشر[جـتك.صـندوق]؛
    عرف صورة: مؤشر[جـتك.صـورة]؛
    عرف لصيقة: مؤشر[جـتك.لـصيقة]؛
    صندوق_أفقي = جـتك.صـندوق.أنشئ(جـتك.اتـجاه._أفقي_، 20)؛
    لصيقة = جـتك.لـصيقة.أنشئ("السلام عليكم")؛
    صورة = جـتك.صـورة.أنشئ("../../Doc/Resources/logo.gif")؛
    جـتك.صـندوق.احشد_في_المقدمة(صندوق_أفقي، لصيقة، 1، 0، 0)؛
    جـتك.صـندوق.احشد_في_المؤخرة(صندوق_أفقي، صورة، 1، 0، 0)؛
    جـتك.صـندوق.احشد_في_المقدمة(صندوق، صندوق_أفقي، 1، 0، 0)؛
    أرجع لصيقة؛
  };
  
  عرف أنشئ_حقلا: دالة (صندوق: مؤشر[جـتك.صـندوق]، لصيقة: مؤشر[جـتك.لـصيقة])
  {
    عرف حقل: مؤشر[جـتك.حـقل]؛
    حقل = جـتك.حـقل.أنشئ()؛
    
    جـتك.صـندوق.احشد_في_المقدمة(صندوق، حقل، 1، 0، 0)؛
    جـلب.اربط_إشارة(حقل، "changed"، قد_تغير_الحقل~مؤشر~مثل[مؤشر[دالة]]، لصيقة، 0، 0)؛
  };

  عرف أنشئ_مساحة_الكتابة: دالة (صندوق: مؤشر[جـتك.صـندوق]) => مؤشر[جـتك.صـوان_نصي]
  {
    عرف مساحة_نصية: مؤشر[جـتك.مـساحة_نصية]؛
    عرف صوان_نصي: مؤشر[جـتك.صـوان_نصي]؛
    صوان_نصي = جـتك.صـوان_نصي.أنشئ(0)؛
    مساحة_نصية = جـتك.مـساحة_نصية.أنشئ(صوان_نصي)؛
    جـتك.مـساحة_نصية.عين_نمط_اللف(مساحة_نصية، جـتك.نـمط_اللف._كلمة_)؛
    جـتك.صـندوق.احشد_في_المقدمة(صندوق، مساحة_نصية، 1، 1، 0)؛
    أرجع صوان_نصي؛
  };

  عرف أنشئ_أزرار_مساحة_الكتابة: دالة (صندوق: مؤشر[جـتك.صـندوق]، صوان: مؤشر[جـتك.صـوان_نصي])
  {
    عرف صندوق_الأزرار: مؤشر[جـتك.صـندوق_أزرار]؛
    عرف زر_الحوارية: مؤشر[جـتك.الـزر]؛
    عرف زر_المحو: مؤشر[جـتك.الـزر]؛
    صندوق_الأزرار = جـتك.صـندوق_أزرار.أنشئ(جـتك.اتـجاه._أفقي_)؛
    جـتك.صـندوق_أزرار.عين_النسق(صندوق_الأزرار، جـتك.طـراز_صندوق_الأزرار._انتشار_)؛
    زر_الحوارية = جـتك.الـزر.أنشئ_بلصيقة("قُلها")؛
    جـلب.اربط_إشارة(زر_الحوارية، "clicked"، قد_نقر_زر_الحوارية~مؤشر~مثل[مؤشر[دالة]]، صوان، 0، 0)؛
    جـتك.حـاوية.أضف(صندوق_الأزرار، زر_الحوارية)؛
    زر_المحو = جـتك.الـزر.أنشئ_بلصيقة("أمحُ")؛
    جـلب.اربط_إشارة(زر_المحو، "clicked"، قد_نقر_زر_المحو~مؤشر~مثل[مؤشر[دالة]]، صوان، 0، 0)؛
    جـتك.حـاوية.أضف(صندوق_الأزرار، زر_المحو)؛
    جـتك.صـندوق.احشد_في_المقدمة(صندوق، صندوق_الأزرار، 1، 0، 0)؛
  };

  عرف أنشئ_الزر_الثنائي: دالة (صندوق: مؤشر[جـتك.صـندوق]، لصيقة: مؤشر[جـتك.لـصيقة])
  {
    عرف زر: مؤشر[جـتك.الـزر_الثنائي]؛
    زر = جـتك.الـزر_الثنائي.أنشئ("بدّل")؛
    جـتك.صـندوق.احشد_في_المقدمة(صندوق، زر، 1، 0، 0)؛
    جـلب.اربط_إشارة(زر، "toggled"، قد_بدل_الزر_الثنائي~مؤشر~مثل[مؤشر[دالة]]، لصيقة، 0، 0)؛
  };

  عرف أنشئ_خانة_التأشير: دالة (صندوق: مؤشر[جـتك.صـندوق]، لصيقة: مؤشر[جـتك.لـصيقة])
  {
    عرف خانة: مؤشر[جـتك.خـانة_تأشير]؛
    خانة = جـتك.خـانة_تأشير.أنشئ("أشّر")؛
    جـتك.صـندوق.احشد_في_المقدمة(صندوق، خانة، 1، 0، 0)؛
    جـلب.اربط_إشارة(خانة، "toggled"، قد_بدل_الزر_الثنائي~مؤشر~مثل[مؤشر[دالة]]، لصيقة، 0، 0)؛
  };

  عرف قد_تغير_اختيار_القائمة: دالة (قائمة: مؤشر[جـتك.قـائمة]، عنصر: مؤشر[جـتك.سـلة]، لصيقة: مؤشر[جـتك.لـصيقة])
  {
    عرف محارف: مصفوفة[محرف، 100]؛
    مـتم.مـحارف.اطبع(محارف~مؤشر، "العنصر المختار: %s"، جـتك.لـصيقة.هات_النص(جـتك.سـلة.هات_ابن(عنصر)~مثل[مؤشر[جـتك.لـصيقة]]))؛
    جـتك.لـصيقة.عين_النص(لصيقة، محارف~مؤشر)؛
  };

  عرف قد_تغير_الحقل: دالة (قابل_للتعديل: مؤشر[جـتك.قـابل_للتعديل]، لصيقة: مؤشر[جـتك.لـصيقة])
  {
    عرف محارف: مصفوفة[محرف، 100]؛
    مـتم.مـحارف.اطبع(محارف~مؤشر، "النص المدخل: %s"، جـتك.قـابل_للتعديل.هات_محارف(قابل_للتعديل، 0، -1))؛
    جـتك.لـصيقة.عين_النص(لصيقة، محارف~مؤشر)؛
  };

  عرف قد_نقر_زر_حول: دالة (ودجة: مؤشر[جـتك.ودجـة]، معطيات: مؤشر)
  {
    عرف حوارية: مؤشر[جـتك.حـوارية_رسالة]؛
    حوارية = جـتك.حـوارية_رسالة.أنشئ(
      النافذة، 0، جـتك.صـنف_رسالة._معلومة_، جـتك.نـوع_الزر._أغلق_، "الأسس: دليل ودجات جتك.\جبرمجة: سرمد خالد عبد الله"
    )؛
    جـتك.حـوارية.شغل(حوارية)؛
    جـتك.ودجـة.مزق(حوارية)؛
  };

  عرف قد_نقر_زر_الحوارية: دالة (ودجة: مؤشر[جـتك.ودجـة]، صوان_نصي: مؤشر[جـتك.صـوان_نصي])
  {
    // هات النص من الصوان.
    عرف نص: مؤشر[محرف]؛
    عرف مكرر_البداية: جـتك.مـكرر_نصي؛
    عرف مكرر_النهاية: جـتك.مـكرر_نصي؛
    جـتك.صـوان_نصي.هات_مكرر_البداية(صوان_نصي، مكرر_البداية~مؤشر)؛
    جـتك.صـوان_نصي.هات_مكرر_النهاية(صوان_نصي، مكرر_النهاية~مؤشر)؛
    نص = جـتك.صـوان_نصي.هات_النص(صوان_نصي، مكرر_البداية~مؤشر، مكرر_النهاية~مؤشر، 1)؛

    عرف حوارية: مؤشر[جـتك.حـوارية_رسالة]؛
    حوارية = جـتك.حـوارية_رسالة.أنشئ(
      النافذة، 0، جـتك.صـنف_رسالة._معلومة_، جـتك.نـوع_الزر._أغلق_، نص
    )؛
    جـتك.حـوارية.شغل(حوارية)؛
    جـتك.ودجـة.مزق(حوارية)؛
  };

  عرف قد_نقر_زر_المحو: دالة (ودجة: مؤشر[جـتك.ودجـة]، صوان_نصي: مؤشر[جـتك.صـوان_نصي])
  {
    جـتك.صـوان_نصي.عين_النص(صوان_نصي، ""، 0)؛
  };

  عرف قد_بدل_الزر_الثنائي: دالة (زر: مؤشر[جـتك.الـزر_الثنائي]، لصيقة: مؤشر[جـتك.لـصيقة])
  {
    عرف فعال: ثنائي؛
    فعال = جـتك.الـزر_الثنائي.أهو_فعال(زر)؛
    إذا فعال جـتك.لـصيقة.عين_النص(لصيقة، "الزر الآن مضغوط")
    وإلا جـتك.لـصيقة.عين_النص(لصيقة، "الزر الآن غير مضغوط")؛
  };

  عرف قد_نقر_زر_الخروج: دالة (ودجة: مؤشر[جـتك.ودجـة]، معطيات: مؤشر)
  {
    جـتك.ودجـة.مزق(النافذة)؛
  };
};

نفذ دلـيل_الودجات.ابدأ؛

