اشمل "جـتك.أسس"؛

عرف الـبسملة: حزمة
{
  عرف النافذة: مؤشر[جـتك.نـافذة_تطبيق]؛

  عرف ابدأ: دالة
  {
    عرف التطبيق: مؤشر[جـتك.تـطبيق]؛
    عرف الحالة: صحيح؛
    عرف معرف_التطبيق: مؤشر[محرف]؛
    معرف_التطبيق = "org.alusus.gtk-basmalah"؛
    التطبيق = جـتك.تـطبيق.أنشئ(معرف_التطبيق، 0)؛
    جـتك.اربط_إشارة(التطبيق، "activate"، فعّل~مؤشر~مثل[مؤشر[دالة]]، 0، 0، 0)؛
    الحالة = جـتك.تـطبيق.شغل(التطبيق، 0، 0)؛
    جـتك.حرر(التطبيق)؛
  };

  عرف فعّل: دالة (تطبيق: مؤشر[جـتك.تـطبيق]، معلومات: مؤشر)
  {
    عرف صندوق: مؤشر[جـتك.صـندوق]؛
    عرف لصيقة: مؤشر[جـتك.لـصيقة]؛
    عرف صندوق_الأزرار: مؤشر[جـتك.صـندوق_أزرار]؛
    عرف زر_حول: مؤشر[جـتك.الـزر]؛
    عرف زر_الخروج: مؤشر[جـتك.الـزر]؛

    النافذة = جـتك.نـافذة_تطبيق.أنشئ(تطبيق)؛
    جـتك.نـافذة.اضبط_العنوان(النافذة، "الأسس/حزمة جـتك: البسملة")؛
    جـتك.نـافذة.اضبط_الحجم_المبدئي(النافذة، 480، 320)؛

    صندوق = جـتك.صـندوق.أنشئ(جـتك.اتـجاه._عمودي_، 20)؛

    لصيقة = جـتك.لـصيقة.أنشئ("بسم الله الرحمن الرحيم")؛

    صندوق_الأزرار = جـتك.صـندوق_أزرار.أنشئ(جـتك.اتـجاه._أفقي_)؛
    جـتك.صـندوق_أزرار.اضبط_النسق(صندوق_الأزرار، جـتك.طـراز_صندوق_الأزرار._انتشار_)؛

    زر_حول = جـتك.الـزر.أنشئ_بلصيقة("حول")؛
    جـتك.اربط_إشارة(زر_حول، "clicked"، قد_نقر_زر_حول~مؤشر~مثل[مؤشر[دالة]]، 0، 0، 0)؛
    جـتك.حـاوية.أضف(صندوق_الأزرار، زر_حول)؛

    زر_الخروج = جـتك.الـزر.أنشئ_بلصيقة("خروج")؛
    جـتك.اربط_إشارة(زر_الخروج، "clicked"، قد_نقر_زر_الخروج~مؤشر~مثل[مؤشر[دالة]]، 0، 0، 0)؛
    جـتك.حـاوية.أضف(صندوق_الأزرار، زر_الخروج)؛

    جـتك.صـندوق.احشد_في_المقدمة(صندوق، لصيقة، 1، 0، 0)؛
    جـتك.صـندوق.احشد_في_المقدمة(صندوق، صندوق_الأزرار، 1، 0، 0)؛
    
    جـتك.حـاوية.أضف(النافذة، صندوق)؛

    جـتك.عـنصر.أظهر_الكل(النافذة)؛
  };

  عرف قد_نقر_زر_حول: دالة (عنصر: مؤشر[جـتك.عـنصر]، معلومات: مؤشر)
  {
    عرف حوارية: مؤشر[جـتك.حـوارية]؛
    حوارية = جـتك.حـوارية_رسالة.أنشئ(
      النافذة، 0، جـتك.صـنف_رسالة._معلومة_، جـتك.نـوع_الزر._أغلق_، "البسملة.\nتطوير: سرمد خالد عبد الله"
    );
    جـتك.حـوارية.شغل(حوارية)؛
    جـتك.عـنصر.مزق(حوارية)؛
  };

  عرف قد_نقر_زر_الخروج: دالة (عنصر: مؤشر[جـتك.عـنصر]، معلومات: مؤشر)
  {
    جـتك.عـنصر.مزق(النافذة)؛
  };
};

نفذ الـبسملة.ابدأ؛

