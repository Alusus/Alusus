import "Srl/Console";
import "Srl/String";
import "Build.alusus";
use Srl;

func factorial (i:Int): Int {
  if i<=0 return 1;
  return i*factorial(i-1);
}

@expname[main]
func start (argCount: Int, args: ptr[array[ptr[array[Char]]]]): Int {
  def i: Int;
  def lastArg: ptr[array[Char]] = args~cnt(argCount - 1);
  if lastArg~cnt(0) >= '0' && lastArg~cnt(0) <= '9' {
    String.scan(lastArg, "%d", i~ptr);
    Console.print("Factorial of %d is %d\n", i, factorial(i));
  } else {
    Console.print("Usage: factorial <number>\n");
  }
  return 0;
}

if Process.argCount == 3 {
  def lastArg: ptr[array[Char]] = Process.args~cnt(Process.argCount - 1);
  if lastArg~cnt(0) >= '0' && lastArg~cnt(0) <= '9' {
    start(Process.argCount, Process.args);
  } else {
    if Build.genExecutable(start~ast, lastArg) {
      Console.print("Compilation complete.\n");
    };
  }
} else {
  Console.print("JIT usage: alusus factorial.alusus <number>\n");
  Console.print("Generate executable: alusus factorial.alusus <output-filename>\n");
}
