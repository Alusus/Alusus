import "Srl/Net";
import "Srl/Console";
import "Srl/Memory";
import "Srl/String";
import "Srl/Array";
import "Srl/System";

@expname[atof] func parseFloat(CharsPtr): Float[64];

use Srl;

func getAverageHeight(csv: String, count: ref[Int], avg: ref[Float[64]]) {
  def lines: Array[String] = csv.split("\n");
  if lines.getLength() < 2 {
      Console.print("Invalid data received:\n%s\n", csv.buf);
      System.exit(1);
  }
  count = 0;
  def total: Float[64] = 0;
  def i: Int;
  // We will skip the header and start from the second line.
  for i = 1, i < lines.getLength(), ++i {
    if lines(i).trim() == "" continue;
    ++count;
    total += parseFloat(lines(i).split(",")(1));
  }
  average = total / count;
}

def data: CharsPtr;
def size: Int;
def count: Int;
def average: Float[64];
if Net.get("https://people.sc.fsu.edu/~jburkardt/data/csv/hw_25000.csv", data~ptr, size~ptr) {
  Console.print("CSV fetched from web\n");
  def str: String = data;
  Memory.free(data);
  getAverageHeight(str, count, average);
  Console.print("Number of persons: %d, Average height: %f\n", count, average);
} else {
  Console.print("Failed to fetch data from the web.\n");
}
