import "Srl/Net";
import "Srl/Console";
import "Srl/Memory";
import "Srl/String";
import "Srl/Array";

@expname[atoi] func parseInt(CharsPtr): Int;

use Srl;
func countHousesBiggerThan(roomCount: Int, csv: String): Int {
  def lines: Array[String] = csv.split("\r");
  def count: Int = 0;
  def i: Int;
  // We will skip the header and start from the second line.
  for i = 1, i < lines.getLength(), ++i {
    if lines(i).trim() == "" continue;
    if parseInt(lines(i).split(",")(4)) > roomCount { ++count }
  }
  return count;
}

def data: CharsPtr;
def size: Int;
if Net.get("http://samplecsvs.s3.amazonaws.com/Sacramentorealestatetransactions.csv", data~ptr, size~ptr) {
  Console.print("CSV fetched from web\n");
  def str: String = data;
  Memory.free(data);
  def count: Int = countHousesBiggerThan(3, str);
  Console.print("Houses with more than 3 rooms: %d\n", count);
} else {
  Console.print("Failed to fetch data from the web\n");
}
