/**
 * @file Srl/String.alusus
 * Contains the class Srl.String.
 *
 * @copyright Copyright (C) 2023 Sarmad Khalid Abdullah
 *
 * @license This file is released under Alusus Public License, Version 1.0.
 * For details on usage and copying conditions read the full license in the
 * accompanying license file or at <https://alusus.org/license.html>.
 */
//==============================================================================

import "srl";
import "Memory";
import "Array";

@merge module Srl
{
    class StringBase [T: type] {
        //============
        // Member Vars

        def refCount: ref[Int[32]];
        def buf: ptr[array[T]];

        //===============
        // Initialization

        handler this~init() this._init();

        handler this~init(str: ref[StringBase[T]]) {
            this._init();
            this.assign(str);
        };

        handler this~init(buf: ptr[array[T]]) {
            this._init();
            this.assign(buf);
        };

        handler this~init(buf: ptr[array[T]], n: ArchInt) {
            this._init();
            this.assign(buf, n);
        };

        handler this~init(skipCopying: Bool, buf: ptr[array[T]]) {
            this._init();
            if skipCopying this.buf = buf
            else this.assign(buf);
        };

        handler this~terminate() this._release();

        //=================
        // Member Functions

        handler this._init() {
            @shared def strTerminator: T(0);
            this.buf = strTerminator~ptr~cast[ptr[array[T]]];
            this.refCount~ptr = 0;
        };

        handler this._alloc (length: ArchInt) {
            if length < 0 length = 0;
            this.refCount~ptr = Memory.alloc(Int[32]~size + T~size * (length + 1))~cast[ptr[Int[32]]];
            this.buf = (this.refCount~ptr + 1)~cast[ptr[array[T]]];
            this.refCount = 1;
        };

        handler this._realloc (newLength: ArchInt) {
            if newLength < 0 newLength = 0;
            this.refCount~ptr = Memory.realloc(
                this.refCount~ptr, Int[32]~size + T~size * (newLength + 1)
            )~cast[ptr[Int[32]]];
            this.buf = (this.refCount~ptr + 1)~cast[ptr[array[T]]];
        };

        handler this._release() {
            if this.refCount~ptr != 0 {
                --this.refCount;
                if this.refCount == 0 Memory.free(this.refCount~ptr);
            };
            this._init();
        };

        handler this.getLength ():ArchInt {
            return getLength(this.buf);
        };

        handler this.alloc (length: ArchInt) {
            this._release();
            this._alloc(length);
        };

        handler this.realloc (newLength: ArchInt) {
            if this.refCount~ptr == 0 this._alloc(newLength)
            else this._realloc(newLength);
        }

        handler this.assign (str: ref[StringBase[T]]) {
            this._release();
            this.refCount~ptr = str.refCount~ptr;
            this.buf = str.buf;
            if this.refCount~ptr != 0 {
                ++this.refCount;
            };
        };

        handler this.assign (buf: ptr[array[T]]) {
            this._release();
            if buf != 0 {
                this._alloc(getLength(buf));
                copy(this.buf, buf);
            }
        };

        handler this.assign (buf: ptr[array[T]], n: ArchInt) {
            this._release();
            this._alloc(n);
            copy(this.buf, buf, n);
            this.buf~cnt(n) = 0;
        };

        handler this.append (buf: ptr[array[T]]) {
            this.append(buf, getLength(buf));
        };

        handler this.append (buf: ptr[array[T]], n: ArchInt) {
            def bufLen: ArchInt = 0;
            while bufLen < n and buf~cnt(bufLen) != 0 { ++bufLen };
            if bufLen == 0 return;
            def thisBufLen: ArchInt = this.getLength();
            if thisBufLen == 0 {
                this.assign(buf, n);
                return;
            };
            def newLength: ArchInt = thisBufLen + bufLen;
            if this.refCount~ptr == 0 {
                def currentBuf: ptr[array[T]] = this.buf;
                this._alloc(newLength);
                copy(this.buf, currentBuf);
                concat(this.buf, buf, n);
            } else if this.refCount > 1 {
                def currentBuf: ptr[array[T]] = this.buf;
                --this.refCount;
                this._alloc(newLength);
                copy(this.buf, currentBuf);
                concat(this.buf, buf, n);
            } else {
                this._realloc(newLength);
                concat(this.buf, buf, n);
            };
        };

        handler this.append (c: T) {
            this.append(c~ptr~cast[ptr[array[T]]], 1);
        };

        handler this.concat (buf: ptr[array[T]]): StringBase[T] {
            def newStr: StringBase[T] = this.buf;
            newStr.append(buf);
            return newStr;
        };

        handler this.concat (buf: ptr[array[T]], n: ArchInt): StringBase[T] {
            def newStr: StringBase[T] = this.buf;
            newStr.append(buf, n);
            return newStr;
        };

        handler this.concat (c: T): StringBase[T] {
            def newStr: StringBase[T] = this.buf;
            newStr.append(c);
            return newStr;
        };

        handler this.concat (i: Int[64]): StringBase[T] {
            def newStr: StringBase[T] = this.buf;
            newStr.append(i);
            return newStr;
        };

        handler this.concat (f: Float[64]): StringBase[T] {
            def newStr: StringBase[T] = this.buf;
            newStr.append(f);
            return newStr;
        };

        handler this.find (buf: ptr[array[T]]): ArchInt {
            return this.find(0, buf);
        };

        handler this.find (startPos: ArchInt, buf: ptr[array[T]]): ArchInt {
            def startBuf: ptr[array[T]] = this.buf;
            while startBuf~cnt(0) != 0 && startPos > 0 {
                --startPos;
                startBuf = startBuf~cnt(1)~ptr~cast[ptr[array[T]]];
            }
            if startPos > 0 return -1;
            def pos: ptr = find(startBuf, buf);
            if pos == 0 return -1;
            return pos~cast[ArchInt] - this.buf~cast[ArchInt];
        };

        handler this.findLast (buf: ptr[array[T]]): ArchInt {
            if this.buf == 0 return -1;
            def pos: ptr = findLast(this.buf, buf);
            if pos == 0 return -1;
            return pos~cast[ArchInt] - this.buf~cast[ArchInt];
        };

        handler this.find (c: T): ArchInt {
            return this.find(0, c);
        };

        handler this.find (startPos: ArchInt, c: T): ArchInt {
            def startBuf: ptr[array[T]] = this.buf;
            while startBuf~cnt(0) != 0 && startPos > 0 {
                --startPos;
                startBuf = startBuf~cnt(1)~ptr~cast[ptr[array[T]]];
            }
            if startPos > 0 return -1;
            def pos: ptr = find(startBuf, c);
            if pos == 0 return -1;
            return pos~cast[ArchInt] - this.buf~cast[ArchInt];
        };

        handler this.findLast (c: T): ArchInt {
            if this.buf == 0 return -1;
            def pos: ptr = findLast(this.buf, c);
            if pos == 0 return -1;
            return pos~cast[ArchInt] - this.buf~cast[ArchInt];
        };

        handler this.compare (s: ptr[array[T]]): Int {
            return compare(this.buf, s);
        };

        handler this.compare(s: ptr[array[T]], n: ArchInt): Int {
            return compare(this.buf, s, n);
        };

        handler this.replace (match: ptr[array[T]], replacement: ptr[array[T]]): StringBase[T] {
            def str: StringBase[T];
            def matchLength: ArchInt = getLength(match);
            def buf: ptr[array[T]] = this.buf;
            while 1 {
                def found: ptr[array[T]] = find(buf, match)~cast[ptr[array[T]]];
                if found == 0 {
                    str.append(buf);
                    return str;
                };
                def n: ArchInt = found~cast[ArchInt] - buf~cast[ArchInt];
                str.append(buf, n);
                str.append(replacement);
                buf = found~cnt(matchLength)~ptr~cast[ptr[array[T]]];
            };
            return str;
        };

        handler this.trim (): StringBase[T] {
            return this._trim(true, true);
        };

        handler this.trimStart (): StringBase[T] {
            return this._trim(true, false);
        };

        handler this.trimEnd (): StringBase[T] {
            return this._trim(false, true);
        };

        handler this._trim (trimStart: Bool, trimEnd: Bool): StringBase[T] {
            if this.getLength() == 0 return StringBase[T]();
            def begin: ArchInt = 0;
            def end: ArchInt = this.getLength() - 1;
            if trimStart while isSpace(this.buf~cnt(begin)) { ++begin };
            if trimEnd while isSpace(this.buf~cnt(end)) { --end };
            if end >= begin {
                def str: StringBase[T];
                str.assign(this.buf~cnt(begin)~ptr~cast[ptr[array[T]]], end - begin + 1);
                return str;
            } else {
                return StringBase[T]();
            };
        };

        handler this.toUpperCase(): StringBase[T] {
            def str: StringBase[T] = StringBase[T]();
            def charIndex: ArchInt;
            for charIndex=0, charIndex<this.getLength(), charIndex++ {
                str += toUpper(this(charIndex)~cast[T])~cast[T];
            };
            return str;
        }

        handler this.toLowerCase(): StringBase[T] {
            def str: StringBase[T] = StringBase[T]();
            def charIndex: ArchInt;
            for charIndex=0, charIndex<this.getLength(), charIndex++ {
                str += toLower(this(charIndex)~cast[T])~cast[T];
            };
            return str;
        }

        handler this.slice (begin: ArchInt, count: ArchInt): StringBase[T] {
            def str: StringBase[T];
            def l: ArchInt = this.getLength();
            if begin >= l return str;
            str.assign(this.buf~cnt(begin)~ptr~cast[ptr[array[T]]], count);
            return str;
        };

        handler this.split (separator: ptr[array[T]]): Array[StringBase[T]] {
            def ary: Array[StringBase[T]];
            def str: StringBase[T];
            def matchLength: ArchInt = getLength(separator);
            def buf: ptr[array[T]] = this.buf;
            while 1 {
                def found: ptr[array[T]] = find(buf, separator)~cast[ptr[array[T]]];
                if found == 0 {
                    str.assign(buf);
                    ary.add(str);
                    return ary;
                };
                def n: ArchInt = found~cast[ArchInt] - buf~cast[ArchInt];
                if n == 0 str = StringBase[T]() else str.assign(buf, n);
                ary.add(str);
                buf = found~cnt(matchLength)~ptr~cast[ptr[array[T]]];
            };
            return ary;
        };

        func merge (ary: Array[StringBase[T]], separator: ptr[array[T]]): StringBase[T] {
            def str: StringBase[T];
            def i: ArchInt;
            for i = 0, i < ary.getLength(), ++i {
                if i != 0 str += separator;
                str += ary(i);
            };
            return str;
        };

        func merge (ary: Array[ptr[array[T]]], separator: ptr[array[T]]): StringBase[T] {
            def str: StringBase[T];
            def i: ArchInt;
            for i = 0, i < ary.getLength(), ++i {
                if i != 0 str += separator;
                str += ary(i);
            };
            return str;
        };

        handler this.clear() {
            this._release();
        }

        //==========
        // Operators

        handler this = ref[StringBase[T]] this.assign(value);
        handler this = ptr[array[T]] this.assign(value);

        handler this~cast[ptr[array[T]]] return this.buf;

        handler this + ptr[array[T]]: StringBase[T] return this.concat(value);
        handler this + T: StringBase[T] return this.concat(value);
        handler this + Int[64]: StringBase[T] return this.concat(value);
        handler this + Float[64]: StringBase[T] return this.concat(value);

        handler this += ptr[array[T]] this.append(value);
        handler this += T this.append(value);
        handler this += Int[64] this.append(value);
        handler this += Float[64] this.append(value);

        handler this(i: ArchInt): T {
            if i < 0 return 0;
            return this.buf~cnt(i);
        };

        handler this == ptr[array[T]]: Bool return this.compare(value) == 0;
        handler this != ptr[array[T]]: Bool return this.compare(value) != 0;
        handler this > ptr[array[T]]: Bool return this.compare(value) > 0;
        handler this < ptr[array[T]]: Bool return this.compare(value) < 0;
        handler this >= ptr[array[T]]: Bool return this.compare(value) >= 0;
        handler this <= ptr[array[T]]: Bool return this.compare(value) <= 0;

        //=================
        // Static Functions

        func isSpace (c: T): Bool {
            return c != 0 && (c == ' ' || c == '\n' || c == '\r' || c == '\t');
        };

        func isEqual(s1: ptr[array[T]], s2: ptr[array[T]]): Bool {
            return compare(s1, s2) == 0;
        };

        func remove(chrs: ptr[array[T]], chr: T): ptr[array[T]] {
            def pointer: ptr[array[T]] = find(chrs, chr);
            while pointer != 0 {
                copy(pointer, pointer~cnt(1)~ptr~cast[ptr[array[T]]]);
                pointer = find(pointer, chr);
            };
            return chrs;
        };

        func replace(chrs: ptr[array[T]], fromCharacter: T, toCharacter: T): ptr[array[T]] {
            def pointer: ptr[T] = find(chrs, fromCharacter);
            while pointer != 0 {
                pointer~cnt = toCharacter;
                pointer = find((pointer + 1)~cast[ptr[array[T]]], fromCharacter);
            };
            return chrs;
        };

        func charAt(chrs: ptr[array[T]], index: Int): ptr[array[T]] {
            @shared
            def buffer: array[T, 2];
            copy(buffer~ptr, chrs~cnt(index)~ptr~cast[ptr[array[T]]], 1);
            buffer(1) = 0;
            return buffer~ptr;
        };

        func findLast(haystack: ptr[T], needle: ptr[T]): ptr[T] {
            if (needle~cnt == 0) return haystack;

            def result: ptr[T] = 0;
            while 1 {
                def p: ptr[T] = find(haystack, needle);
                if p == 0 break;
                result = p;
                haystack = p + 1;
            }

            return result;
        }
    };
};

@merge module Srl
{
    def String: alias StringBase[Char];

    @merge class String {
        handler this.append (i: Int[64]) {
            this._append(i, 1);
        }

        handler this._append (i: Int[64], minDigits: Int) {
            def buf: array[Char, 22];
            def strptr: ptr[Char] = buf(19)~ptr;
            strptr~cnt = 0;
            def negative: Bool = 0;
            if i < 0 {
                negative = 1;
                i = -i;
            }
            while minDigits-- > 0 || i > 0 {
                strptr = strptr - 1;
                strptr~cnt = '0' + i % 10;
                i /= 10;
            }
            if negative {
                strptr = strptr - 1;
                strptr~cnt = '-';
            }
            this.append(strptr~cast[CharsPtr]);
        };

        handler this.append (f: Float[64]) {
            if f < 0 {
                f *= -1;
                this.append('-');
            }
            this.append(f~cast[Int[64]]);
            def fractions: Float[64] = f - f~cast[Int[64]];
            this.append('.');
            this._append((fractions * 1000000.0f64 + 0.5f64)~cast[Int[64]], 6);
        };

        @expname[strchr]
        func find(s: ptr[Char], c: Int): ptr[Char];

        @expname[memchr]
        func find(s: ptr[Void], c: Int, n: ArchInt): ptr[Void];

        @expname[strrchr]
        func findLast(s: ptr[Char], c: Int): ptr[Char];

        @expname[strstr]
        func find(haystack: ptr[Char], needle: ptr[Char]): ptr[Char];

        @expname[strcmp]
        func compare(s1: ptr[Char], s2: ptr[Char]): Int;

        @expname[strncmp]
        func compare(s1: ptr[Char], s2: ptr[Char], n: ArchInt): Int;

        @expname[strcpy]
        func copy(dest: ptr[Char], src: ptr[Char]): ptr[Void];

        @expname[strncpy]
        func copy(dest: ptr[Char], src: ptr[Char], n: ArchInt): ptr[Void];

        @expname[strcat]
        func concat(dest: ptr[Char], src: ptr[Char]): ptr[Char];

        @expname[strncat]
        func concat(dest: ptr[Char], src: ptr[Char], n: ArchInt): ptr[Char];

        @expname[strlen]
        func getLength(s: ptr[Char]): ArchInt;

        @expname[sprintf]
        func assign(target: ptr[Char], format: ptr[Char], args: ...any): Int[32];

        @expname[sscanf]
        func scan(source: ptr[Char], format: ptr[Char], args: ...any): Int[32];

        func toUpper(c: int): int {
            if c >= 'a' && c <= 'z' return c - 'a' + 'A'
            else return c;
        }

        func toLower(c: int): int {
            if c >= 'A' && c <= 'Z' return c - 'A' + 'a'
            else return c;
        }

        func format(format: ptr[array[Char]], args: ...any): String {
            def str: String;
            while 1 {
                def pos: ptr[Char] = find(format, '%');
                if pos == 0 {
                    str += format;
                    break;
                } else {
                    str.append(format, pos~cast[ArchInt] - format~cast[ArchInt]);
                    def c: Char = (pos + 1)~cnt;
                    if c == 's' str += args~next_arg[ptr[array[Char]]]
                    else if c == 'c' str += args~next_arg[Char]
                    else if c == 'i' str += args~next_arg[Int]
                    else if c == 'l' str += args~next_arg[Int[64]]
                    else if c == 'f' str += args~next_arg[Float]
                    else if c == 'd' str += args~next_arg[Float[64]]
                    else if c == '%' str += '%';
                    format = (pos + 2)~cast[CharsPtr];
                }
            }
            return str;
        }

        func parseInt (chars: ptr[array[Char]]): Int[64] {
            def negative: Bool(0);
            def result: Int[64](0);
            def charPtr: ptr[Char](chars);
            if charPtr~cnt == '-' {
                negative = 1;
                charPtr = charPtr + 1;
            }
            while charPtr~cnt >= '0' && charPtr~cnt <= '9' {
                result *= 10;
                result += charPtr~cnt - '0';
                charPtr = charPtr + 1;
            }
            if negative return -result else return result;
        }

        func parseFloat (chars: ptr[array[Char]]): Float[64] {
            def negative: Bool(0);
            def result: Float[64](0);
            def charPtr: ptr[Char](chars);
            if charPtr~cnt == '-' {
                negative = 1;
                charPtr = charPtr + 1;
            }
            while charPtr~cnt >= '0' && charPtr~cnt <= '9' {
                result *= 10;
                result += charPtr~cnt - '0';
                charPtr = charPtr + 1;
            }
            if charPtr~cnt == '.' {
                charPtr = charPtr + 1;
                def multiplier: Float[64](1);
                while charPtr~cnt >= '0' && charPtr~cnt <= '9' {
                    multiplier /= 10;
                    result += multiplier * (charPtr~cnt - '0');
                    charPtr = charPtr + 1;
                }
            }
            if negative && result != 0 return -result else return result;
        }

        func parseHexDigit(letter: Char): Int {
            if letter >= '0' and letter <= '9' return letter - '0'
            else if letter >= 'a' and letter <= 'f' return 10 + letter - 'a'
            else if letter >= 'A' and letter <= 'F' return 10 + letter - 'A'
            else return 0;
        }
    };
};
