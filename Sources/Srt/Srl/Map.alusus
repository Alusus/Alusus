/**
 * @file Srl/Map.alusus
 * Contains the Srl.Map type.
 *
 * @copyright Copyright (C) 2021 Sarmad Khalid Abdullah
 *
 * @license This file is released under Alusus Public License, Version 1.0.
 * For details on usage and copying conditions read the full license in the
 * accompanying license file or at <https://alusus.org/license.html>.
 */
//==============================================================================

import "Array";
import "ArrayIndex";
import "System";

@merge module Srl
{
    type Map [T1: type, T2: type] {
        //=================
        // Member Variables

        def keys: Array[T1];
        def values: Array[T2];
        def keysIndex: ref[ArrayIndex[T1]];

        //===============
        // Initialization

        handler this~init() {
            this.keysIndex~ptr = 0;
        };

        handler this~init(useIndex: Bool) {
            if useIndex this.keysIndex~no_deref = ArrayIndex[T1].constructToNew(this.keys)
            else this.keysIndex~ptr = 0;
        };

        handler this~init(map: ref[Map[T1, T2]]) {
            this.keysIndex~ptr = 0;
            this.keys = map.keys;
            this.values = map.values;
        };

        handler this~init(map: ref[Map[T1, T2]], useIndex: Bool) {
            this.keys = map.keys;
            this.values = map.values;
            if useIndex {
                this.keysIndex~no_deref = ArrayIndex[T1].constructToNew(this.keys);
                if map.keysIndex~ptr != 0 this.keysIndex.indices = map.keysIndex.indices
                else this.keysIndex.add(-1);
            } else {
                this.keysIndex~ptr = 0;
            }
        };

        handler this~terminate() {
            if this.keysIndex~ptr != 0 ArrayIndex[T1].release(this.keysIndex);
        };

        //==========
        // Operators

        handler this = ref[Map[T1, T2]] {
            this.keys = value.keys;
            this.values = value.values;
            if this.keysIndex~ptr != 0 {
                if value.keysIndex~ptr != 0 this.keysIndex.indices = value.keysIndex.indices
                else {
                    this.keysIndex.clear();
                    this.keysIndex.add(-1);
                }
            }
        };

        handler this(key: T1): ref[T2] {
            def i: ArchInt = this.findPos(key);
            if i == -1 {
                i = this.keys.getLength();
                this.keys.add(key);
                this.values.add(T2());
                if this.keysIndex~ptr != 0 this.keysIndex.add(-1);
            }
            return this.values(i);
        };

        //=================
        // Member Functions

        handler this.keyAt(i: ArchInt): ref[T1] {
            if i < 0 || i >= this.keys.getLength() {
                System.fail(1, "Argument `i` is out of range.");
            }
            return this.keys(i);
        }

        handler this.valAt(i: ArchInt): ref[T2] {
            if i < 0 || i >= this.keys.getLength() {
                System.fail(1, "Argument `i` is out of range.");
            }
            return this.values(i);
        }

        handler this.set(key: T1, value: T2): ref[Map[T1, T2]] {
            def pos: ArchInt = this.findPos(key);
            if pos == -1 {
                this.keys.add(key);
                this.values.add(value);
                if this.keysIndex~ptr != 0 this.keysIndex.add(-1);
            } else {
                this.values.set(pos, value);
            }
            return this;
        };

        handler this.setAt(i: ArchInt, value: T2): ref[Map[T1, T2]] {
            if i < 0 || i >= this.keys.getLength() {
                System.fail(1, "Argument `i` is out of range.");
            }
            this.values(i) = value;
            return this;
        }

        handler this.insert(i: ArchInt, key: T1, value: T2) {
            this.keys.insert(key);
            this.values.insert(value);
            if this.keysIndex~ptr != 0 this.keysIndex.add(i);
        }

        handler this.remove(key: T1): Bool {
            def pos: ArchInt = this.findPos(key);
            if pos == -1 return false;
            this.removeAt(pos);
            return true;
        };

        handler this.removeAt(i: ArchInt) {
            if i < 0 || i >= this.keys.getLength() {
                System.fail(1, "Argument `i` is out of range.");
            }
            this.keys.remove(i);
            this.values.remove(i);
            if this.keysIndex~ptr != 0 this.keysIndex.remove(i);
        };

        handler this.clear() {
            this.keys.clear();
            this.values.clear();
            if this.keysIndex~ptr != 0 this.keysIndex.clear();
        };

        handler this.getLength(): ArchInt {
            return this.keys.getLength();
        };

        handler this.findPos (key: T1): ArchInt {
            if this.keysIndex~ptr == 0 {
                return this.keys.findPos(key);
            } else {
                return this.keysIndex.findPos(key);
            };
        };
    };
};
