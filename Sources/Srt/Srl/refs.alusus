import "Srl/refs_base";
import "Core/Basic";
import "Core/Data";
import "Spp/Ast";

// Pre-generate shared refs to avoid circular dependency. These types depend on SrdRef in their
// preprocess, while these casting features of SrdRef depend on those types. So we'll pre-generate
// these SrdRef types without these extra casting features to break the circular dependency.
if 1 {
    Srl.SrdRef[Core.Basic.TiObject]();
    Srl.SrdRef[Core.Data.Ast.MergeList]();
    Srl.SrdRef[Core.Data.Ast.LinkOperator]();
    Srl.SrdRef[Core.Data.Ast.List]();
    Srl.SrdRef[Core.Data.Ast.Identifier]();
    Srl.SrdRef[Spp.Ast.Variable]();
}

@merge module Srl {
    @merge class SrdRef {
        preprocess { generateSmartRefAutoImplicitCasts(this_type~ast, T~ast, ast SrdRef) }
    }

    @merge class WkRef {
        preprocess { generateSmartRefAutoImplicitCasts(this_type~ast, T~ast, ast WkRef) }
    }

    func generateSmartRefAutoImplicitCasts(
        originalTp: ref[Core.Basic.TiObject], tp: ref[Core.Basic.TiObject], className: ref[Core.Basic.TiObject]
    ) {
        if not Core.Basic.isDerivedFrom[tp, Spp.Ast.UserType] return;
        def elements: Array[ref[Core.Basic.TiObject]] = Spp.astMgr.findElements(
            (ast elementType == "var"),
            tp,
            Spp.SeekerFlags.SKIP_OWNERS | Spp.SeekerFlags.SKIP_USES | Spp.SeekerFlags.SKIP_CHILDREN
        );
        if elements.getLength() == 0 return;

        if not Spp.astMgr.isInjection(elements(0)) return;

        def varType: ref[Spp.Ast.Type](Spp.astMgr.traceType(elements(0)));
        if varType~ptr == originalTp~ptr return;

        Spp.astMgr.insertAst(
            (ast handler this~cast[ClassName[OutType]] {
                return ClassName[OutType](this.refCounter, castRef[this.obj, OutType]);
            }),
            Map[String, ref[Core.Basic.TiObject]]()
                .set(String("ClassName"), className)
                .set(String("OutType"), Spp.Ast.Variable().{
                    Core.Basic.MapContainerOf[this].setElement("type", varType);
                })
        );

        generateSmartRefAutoImplicitCasts(originalTp, varType, className);
    }

    // Manually define casters for those refs that were pre-generated.
    macro _defineSrdRefCaster [Alias, SrcType, TargetType] {
        def Alias: alias SrdRef[SrcType];
        @merge class Alias {
            handler this~cast[SrdRef[TargetType]] {
                return SrdRef[TargetType](this.refCounter, castRef[this.obj, TargetType]);
            }
        }
    }
    macro _defineSrdRefCaster [Alias, SrcType, TargetType1, TargetType2] {
        def Alias: alias SrdRef[SrcType];
        @merge class Alias {
            handler this~cast[SrdRef[TargetType1]] {
                return SrdRef[TargetType1](this.refCounter, castRef[this.obj, TargetType1]);
            }
            handler this~cast[SrdRef[TargetType2]] {
                return SrdRef[TargetType2](this.refCounter, castRef[this.obj, TargetType2]);
            }
        }
    }
    _defineSrdRefCaster[CoreDataNodeSrdRef, Core.Data.Node, Core.Basic.TiObject];
    _defineSrdRefCaster[CoreDataAstMergeListSrdRef, Core.Data.Ast.MergeList, Core.Data.Node, Core.Basic.TiObject];
    _defineSrdRefCaster[CoreDataAstLinkOperatorSrdRef, Core.Data.Ast.LinkOperator, Core.Data.Node, Core.Basic.TiObject];
    _defineSrdRefCaster[CoreDataAstListSrdRef, Core.Data.Ast.List, Core.Data.Node, Core.Basic.TiObject];
    _defineSrdRefCaster[CoreDataAstIdentifierSrdRef, Core.Data.Ast.Identifier, Core.Data.Node, Core.Basic.TiObject];
    _defineSrdRefCaster[SppAstVariableSrdRef, Spp.Ast.Variable, Core.Data.Node, Core.Basic.TiObject];
}
