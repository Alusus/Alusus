import "globals.alusus";
import "Srl/String.alusus";

@merge module Srl
{
  module Console
  {
    @expname[getchar]
    func getChar(): Int[32];
    
    @expname[putchar]
    func putChar(character: Int[32]): Int[32];

    @expname[printf] func print(format: ptr[Word[8]], args: ...any): Int[32];

    func print(i: Int[64]): Int[32] {
      return print("%lld", i);
    };

    func print(f: Float[64]): Int[32] {
      return print("%f", f);
    };

    func print(f: Float[64], d: Int): Int[32] {
      return print("%.*f", d, f);
    };

    @expname[scanf]
    func scan(format: ptr[Word[8]], args: ...any): Int[32];

    func getInt(): Int {
      def i: Int;
      i = 0;
      scan("%d", i~ptr);
      return i;
    };

    func getFloat(): Float {
      def i: Float;
      i = 0;
      scan("%f", i~ptr);
      return i;
    };

    func getString(str: ptr[array[Char]], count: Word): Void {
      def i: Word;
      def c: Char;
      i = 0;
      c = 0;
      while c != '\n' {
        c = getChar();
        if c != '\n' && i < count - 1 {
          str~cnt(i) = c;
          ++i;
        };
      };
      str~cnt(i) = 0;
    };

    module Style {
      def RESET: "\x1b[0m";
      def BRIGHT: "\x1b[1m";
      def DIM: "\x1b[2m";
      def ITALIC: "\x1b[3m";
      def UNDERSCORE: "\x1b[4m";
      def BLINK: "\x1b[5m";
      def REVERSE: "\x1b[7m";
      def HIDDEN: "\x1b[8m";
      
      // frontground
      def FG_BLACK: "\x1b[30m";
      def FG_RED: "\x1b[31m";
      def FG_GREEN: "\x1b[32m";
      def FG_YELLOW: "\x1b[33m";
      def FG_BLUE: "\x1b[34m";
      def FG_MAGENTA: "\x1b[35m";
      def FG_CYAN: "\x1b[36m";
      def FG_WHITE: "\x1b[37m";

      // background
      def BG_BLACK: "\x1b[40m";
      def BG_RED: "\x1b[41m";
      def BG_GREEN: "\x1b[42m";
      def BG_YELLOW: "\x1b[43m";
      def BG_BLUE: "\x1b[44m";
      def BG_MAGENTA: "\x1b[45m";
      def BG_CYAN: "\x1b[46m";
      def BG_WHITE: "\x1b[47m";
    };
  };
};
