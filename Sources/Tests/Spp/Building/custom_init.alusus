import "Spp";
import "Srl/Console.alusus";

type A
{
  def i: Int;

  handler this~init() {
    Srl.Console.print("constructed!\n");
    this.i = 1;
  };

  handler this~init(that: ref[A]) {
    Srl.Console.print("constructed2!\n");
    this.i = that.i * 2;
  };

  handler this~terminate() {
    this.i = 0;
    Srl.Console.print("destructed!\n");
  };
};

type B
{
  def a: A;
  def a2: A;
};

type C
{
  def i: Int;
  Srl.Console.print("C constructed!\n");
  i = 3;
};

function testNoReturn
{
  def a: A;
  Srl.Console.print("testNoReturn: %d\n", a.i);
};

function testReturnVoid
{
  def a: A;
  Srl.Console.print("testReturnVoid: %d\n", a.i);
  return;
};

function testReturnNonVoid ()=>Int
{
  def a: A;
  Srl.Console.print("testReturnNonVoid: %d\n", a.i);
  return a.i;
};

function getA () => A
{
  def a: A;
  Srl.Console.print("getA.\n");
  return a;
};

function testReturnA
{
  getA();
  Srl.Console.print("testReturnA.\n");
};

function testLoop
{
  def i: Int = 2;
  while i > 0 {
    def a: A;
    Srl.Console.print("testLoop: %d\n", a.i);
    --i;
  };
  Srl.Console.print("testLoop-out.\n");
};

function testBreak
{
  while 1 {
    def a: A;
    Srl.Console.print("testBreak: %d\n", a.i);
    if 1 == 1 break;
    Srl.Console.print("testBreak-after.\n");
  };
  while 1 {
    def a: A;
    while 1 {
      def b: A;
      Srl.Console.print("testBreak-nested: %d, %d\n", a.i, b.i);
      break 2;
    };
    Srl.Console.print("testBreak-afterOuter.\n");
  };
  Srl.Console.print("testBreak-out.\n");
};

function testContinue
{
  def i: Int;
  for i = 0, i < 1, ++i {
    def a: A;
    Srl.Console.print("testContinue: %d\n", a.i);
    if i == 0 continue;
    Srl.Console.print("testContinue-after.\n");
  };
  for i = 0, i < 1, ++i {
    def a: A;
    while 1 {
      def b: A;
      Srl.Console.print("testContinue-nested: %d, %d\n", a.i, b.i);
      continue 2;
    };
    Srl.Console.print("testContinue-afterOuter.\n");
  };
  Srl.Console.print("testContinue-out.\n");
};

function testAutoInit
{
  def b: B;
  Srl.Console.print("testAutoInit: %d, %d\n", b.a.i, b.a2.i);
};

function testInlinedInit
{
  def gc: C;
  Srl.Console.print("testInlinedInit: %d\n", gc.i);
};

function testMissingAssignOp
{
  def a1: A;
  def a2: A;
  a1 = a2;
}

Spp.buildMgr.dumpLlvmIrForElement(testNoReturn~ast);
Spp.buildMgr.dumpLlvmIrForElement(testReturnVoid~ast);
Spp.buildMgr.dumpLlvmIrForElement(testReturnNonVoid~ast);
Spp.buildMgr.dumpLlvmIrForElement(testReturnA~ast);
Spp.buildMgr.dumpLlvmIrForElement(testLoop~ast);
Spp.buildMgr.dumpLlvmIrForElement(testBreak~ast);
Spp.buildMgr.dumpLlvmIrForElement(testContinue~ast);
Spp.buildMgr.dumpLlvmIrForElement(testAutoInit~ast);
Spp.buildMgr.dumpLlvmIrForElement(testInlinedInit~ast);
Spp.buildMgr.dumpLlvmIrForElement(testMissingAssignOp~ast);
