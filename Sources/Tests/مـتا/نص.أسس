اشمل "مـتا/نـص.أسس"؛
اشمل "مـتا/مـصفوفة.أسس"؛
اشمل "مـتم/طـرفية.أسس"؛

عرف رئـيسي: وحدة
{
  دالة اختبر_المشتركة {
    // بحث
    مـتم.طـرفية.اطبع("جد '-': %s\ج"، مـتا.نـص.جد("123 - 456 - 789"، '-'))؛
    مـتم.طـرفية.اطبع("جد '-'، 4: %d\ج"، مـتا.نـص.جد("123 - 456 - 789"، '-'، 4)~مثل[صـحيح[64]] != 0)؛
    مـتم.طـرفية.اطبع("جد '-'، 5: %d\ج"، مـتا.نـص.جد("123 - 456 - 789"، '-'، 5)~مثل[صـحيح[64]] != 0)؛
    مـتم.طـرفية.اطبع("جد_آخر '-': %s\ج"، مـتا.نـص.جد_آخر("123 - 456 - 789"، '-'))؛
    مـتم.طـرفية.اطبع("جد \"124\": %s\ج"، مـتا.نـص.جد("123 - 124 - 125"، "124"))؛

    // مقارنة
    مـتم.طـرفية.اطبع("قارن \"123\" مع \"123\": %d\ج"، مـتا.نـص.قارن("123"، "123"))؛
    مـتم.طـرفية.اطبع("قارن \"123\" مع \"1234\": %d\ج"، مـتا.نـص.قارن("123"، "1234"))؛
    مـتم.طـرفية.اطبع("قارن 3 \"123\" مع \"1234\": %d\ج"، مـتا.نـص.قارن("123"، "1234"، 3))؛

    // نسخ
    عرف صوان: مصفوفة[مـحرف، 100]؛
    مـتا.نـص.انسخ(صوان~مؤشر، "-- بسم الله")؛
    مـتم.طـرفية.اطبع("انسخ: %s\ج"، صوان~مؤشر)؛
    مـتا.نـص.انسخ(صوان~مؤشر، "++ لا حول ولا قوة إلا بالله"، 2)؛
    مـتم.طـرفية.اطبع("انسخ 2: %s\ج"، صوان~مؤشر)؛

    // سَلسلة
    مـتا.نـص.سلسل(صوان~مؤشر، "، الحمد لله")؛
    مـتم.طـرفية.اطبع("سلسل: %s\ج"، صوان~مؤشر)؛
    مـتا.نـص.سلسل(صوان~مؤشر، "!!!!!"، 2)؛
    مـتم.طـرفية.اطبع("سلسل 2: %s\ج"، صوان~مؤشر)؛

    // طول
    مـتم.طـرفية.اطبع("هات_الطول: %d\ج"، مـتا.نـص.هات_الطول(صوان~مؤشر))؛
  }؛

  دالة اختبر_الصنف {
    عرف م: مـتا.نـص؛
    م = "لغة"؛
    مـتم.طـرفية.اطبع(م)؛
    مـتم.طـرفية.اطبع("\جالطول: %d\ج"، م.هات_الطول())؛

    عرف ع: صحيح؛
    لكل ع = 0، ع < م.هات_الطول()، ++ع مـتم.طـرفية.اطبع("م(%d) = %c\ج"، ع، م(ع))؛

    مـتم.طـرفية.اطبع(م + " الأسس\ج")؛

    م = "مرحبا {اسم}. هل تود استخدام {اسم} كاسم المستخدم؟\ج"؛
    مـتم.طـرفية.اطبع(م.استبدل("{اسم}"، "يوسف"))؛

    م = "  \ج بسم الله \ج "؛
    مـتم.طـرفية.اطبع("شذب:")؛
    مـتم.طـرفية.اطبع(م.شذب())؛
    مـتم.طـرفية.اطبع("\جشذب_البداية:")؛
    مـتم.طـرفية.اطبع(م.شذب_البداية())؛
    مـتم.طـرفية.اطبع("\جشذب_النهاية:")؛
    مـتم.طـرفية.اطبع(م.شذب_النهاية())؛
    مـتم.طـرفية.اطبع("\جاجتزء:")؛
    مـتم.طـرفية.اطبع(م.اجتزء(4، 6))؛
    مـتم.طـرفية.اطبع("\ج")؛

    إذا م.اجتزء(11، 8) == "الله" مـتم.طـرفية.اطبع("نجحت المقارنة\ج")؛

    عرف مم: مـتا.مـصفوفة[مـتا.نـص] = م.شذب().قطع(" ")؛
    لكل ع = 0، ع < مم.هات_الطول()، ++ع {
      مـتم.طـرفية.اطبع(مم(ع))؛
      مـتم.طـرفية.اطبع("\ج")؛
    }؛

    م = مـتا.نـص.ادمج(مم، "_")؛
    مـتم.طـرفية.اطبع(م)؛
    مـتم.طـرفية.اطبع("\ج")؛

    عرف ترحيب: مـتا.نـص = "Hello World - مع نص عربي"؛
    مـتم.طـرفية.اطبع("الى_اكبر: %s\n", ترحيب.كبر().buf)؛
    مـتم.طـرفية.اطبع("الى_اصغر: %s", ترحيب.صغر().buf)؛
  }؛
}؛

رئـيسي.اختبر_المشتركة()؛
رئـيسي.اختبر_الصنف()؛

