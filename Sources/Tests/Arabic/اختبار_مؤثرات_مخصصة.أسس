اشمل "مـتم/طـرفية.أسس"؛

استخدم مـتم.طـرفية؛

صنف صـنف {
    عرف م: صحيح؛
    عرف ع: صحيح؛

    عملية هذا~هيئ() هذا.م = 11؛
    عملية هذا~أتلف() اطبع("تم الإتلاف\ج")؛

    عملية هذا = صحيح هذا.م = قيمة؛

    عملية هذا = سند[هذا_الصنف] هذا.م = قيمة.م؛

    عملية هذا~مثل[صحيح] أرجع هذا.م؛

    عملية هذا.خصلة: صحيح {
        اطبع("قراءة خصلة\ج")؛
        أرجع هذا.ع؛
    }

    عملية هذا.خصلة=صحيح {
        اطبع("كتابة خصلة\ج")؛
        هذا.ع = قيمة؛ أرجع هذا.ع؛
    }

    @عملية[""]
    عرف خصلة2: مؤشر[@عضو دالة (سند[هذا_الصنف]): صحيح]؛
    دالة خصلة2_متن_قراءة (هذا: سند[هذا_الصنف]): صحيح {
        اطبع("قراءة خصلة2\ج")؛
        أرجع هذا.ع؛
    }
    خصلة2 = خصلة2_متن_قراءة~مؤشر؛

    @عملية["="]
    عرف خصلة2: مؤشر[@عضو دالة (سند[هذا_الصنف], صحيح)]؛
    دالة خصلة2_متن_كتابة (هذا: سند[هذا_الصنف]، قيمة: صحيح) {
        اطبع("كتابة خصلة2\ج")؛
        هذا.ع = قيمة؛
    }
    خصلة2 = خصلة2_متن_كتابة~مؤشر؛
}؛

إذا 1 {
    عرف ص: صـنف؛
    اطبع("ص~هيئ ==> ص.م: %d\ج"، ص.م)؛

    ص = 3؛
    اطبع("ص = 3 ==> ص.م: %d\ج"، ص.م)؛

    عرف ص2: صـنف = ص؛
    اطبع("ص2 = ص ==> ص2.م: %d\ج"، ص2.م)؛

    اطبع("ص~مثل[صحيح] = %d\ج"، ص~مثل[صحيح])؛

    ص.خصلة = 5؛
    اطبع("%d\ج"، ص.خصلة)؛

    ص.خصلة2 = 6؛
    اطبع("%d\ج"، ص.خصلة2)؛
}؛

صنف سـلف {
    عملية هذا.د1(): مؤشر[محرف] كمؤشر {
        أرجع ""؛
    }
}

صنف خـلف1 {
    @حقنة عرف سلف: سـلف؛
    عرف م: صحيح؛
    عملية (هذا:سـلف).د1(): مؤشر[محرف] حدد_مؤشر {
        أرجع "خلف 1\ج"؛
    }
}

صنف خـلف2 {
    @حقنة عرف سلف: سـلف؛
    عرف ن: صحيح؛
    عملية (هذا:سـلف).د1(): مؤشر[محرف] حدد_مؤشر {
        أرجع "خلف 2\ج"؛
    }
}

دالة اطبع_سلف (س: سند[سـلف]) {
    اطبع(س.د1())؛
}

دالة اختبر {
    عرف خ1: خـلف1؛
    عرف خ2: خـلف2؛
    خ1.م = 2؛
    خ2.ن = 7؛
    اطبع_سلف(خ1)؛
    اطبع_سلف(خ2)؛
}
اختبر()؛

