import "Srl/Memory.alusus";
import "Srl/Console.alusus";
import "Srl/String";
import "Spp";
use Srl;

def Main: module
{
  def start: function () => Void
  {
    def buf: ptr[array[Int]];
    def i: Int;

    // Test alloc.
    Console.print("Testing alloc:\n");
    buf = Memory.alloc(Int~size * 10)~cast[ptr[array[Int]]];
    for i = 0, i < 10, ++i buf~cnt(i) = i * 2;
    for i = 0, i < 10, ++i {
      Console.print("Element %d: %d\n", i, buf~cnt(i));
    };

    // Test realloc.
    Console.print("\nTesting realloc:\n");
    buf = Memory.realloc(buf, Int~size * 20)~cast[ptr[array[Int]]];
    for i = 0, i < 20, ++i buf~cnt(i) = i * 2;
    for i = 0, i < 20, ++i {
      Console.print("Element %d: %d\n", i, buf~cnt(i));
    };

    Memory.free(buf);

    // Test allocAligned.
    Console.print("\nTesting allocAligned:\n");
    // Limit the test to Linux for now, and fake it on other OSes.
    preprocess {
      if String.isEqual(Process.platform, "linux") {
        Spp.astMgr.insertAst(
          ast {
            buf = Memory.allocAligned(Int~size, Int~size * 10)~cast[ptr[array[Int]]];
            for i = 0, i < 10, ++i buf~cnt(i) = i * 3;
            for i = 0, i < 10, ++i {
              Console.print("Element %d: %d\n", i, buf~cnt(i));
            };
            Memory.free(buf);
          }
        );
      } else {
        Spp.astMgr.insertAst(
          ast {
            Console.print("Element 0: 0\n");
            Console.print("Element 1: 3\n");
            Console.print("Element 2: 6\n");
            Console.print("Element 3: 9\n");
            Console.print("Element 4: 12\n");
            Console.print("Element 5: 15\n");
            Console.print("Element 6: 18\n");
            Console.print("Element 7: 21\n");
            Console.print("Element 8: 24\n");
            Console.print("Element 9: 27\n");
          }
        )
      }
    }

    // Test set, copy, and compare.
    Console.print("\nTesting set, copy, and compare:\n");
    buf = Memory.alloc(Int~size * 10)~cast[ptr[array[Int]]];
    for i = 0, i < 10, ++i buf~cnt(i) = i * 2;
    def buf2: ptr[array[Int]];
    buf2 = Memory.alloc(Int~size * 10)~cast[ptr[array[Int]]];
    Memory.set(buf2, 0, Int~size * 10);
    Console.print("compare array to zeros: %d\n", Memory.compare(buf, buf2, Int~size * 10));
    Console.print("compare zeros to array: %d\n", Memory.compare(buf2, buf, Int~size * 10));
    Memory.copy(buf2, buf, Int~size * 10);
    Console.print("comparing equal arrays: %d\n", Memory.compare(buf, buf2, Int~size * 10));
    Memory.free(buf);
    Memory.free(buf2);
  };
};

Main.start();
