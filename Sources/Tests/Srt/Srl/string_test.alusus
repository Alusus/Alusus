import "Srl/String";
import "Srl/Console";
use Srl;

def Main: module
{
  func testStatics {
    // Search
    Console.print("find '-': %s\n", String.find("123 - 456 - 789", '-'));
    Console.print("find '-', 4: %d\n", String.find("123 - 456 - 789", '-', 4)~cast[Int[64]] != 0);
    Console.print("find '-', 5: %d\n", String.find("123 - 456 - 789", '-', 5)~cast[Int[64]] != 0);
    Console.print("findLast '-': %s\n", String.findLast("123 - 456 - 789", '-'));
    Console.print("find \"124\": %s\n", String.find("123 - 124 - 125", "124"));

    // Compare
    Console.print("compare \"123\" to \"123\": %d\n", String.compare("123", "123"));
    Console.print("compare \"123\" to \"1234\": %d\n", String.compare("123", "1234"));
    Console.print("compare 3 \"123\" to \"1234\": %d\n", String.compare("123", "1234", 3));

    // Copy
    def buf: array[Char, 100];
    String.copy(buf~ptr, "HELLO!");
    Console.print("copy: %s\n", buf~ptr);
    String.copy(buf~ptr, "hello world!", 6);
    Console.print("copy 2: %s\n", buf~ptr);

    // Concat
    String.concat(buf~ptr, "world");
    Console.print("concat: %s\n", buf~ptr);
    String.concat(buf~ptr, "!!!!!", 2);
    Console.print("concat 2: %s\n", buf~ptr);

    // Length
    Console.print("getLength: %d\n", String.getLength(buf~ptr));

    // Format
    Console.print(
        "format: %s\n%s\n",
        String.format(
            "str: %s, char: %c, int32: %i, int64: %l, float32: %f, float64: %d, %%",
            "abc123", '^', 32i32, 64i64, 32.5f32, 64.5f64
        ).buf,
        String.format(
            "zero int: %i, negative int: %i, zero float: %f, negative float %f", 0i32, -32i32, 0.0f32, -0.0052f32
        ).buf
    );

    // Parsing
    Console.print("parseInt: %d %d %d %d %d %d\n",
        String.parseInt("0"),
        String.parseInt("-0"),
        String.parseInt("4324"),
        String.parseInt("-1443"),
        String.parseInt("abc"),
        String.parseInt("-123abc")
    );
    Console.print("parseFloat: %f %f %f %f %f %f %f\n\n",
        String.parseFloat("0"),
        String.parseFloat("-0"),
        String.parseFloat("123"),
        String.parseFloat("324.76"),
        String.parseFloat("-757"),
        String.parseFloat("abc"),
        String.parseFloat("123abc")
    );
    Console.print("parseHexDigit: %d %d %d %d\n",
        String.parseHexDigit('7'),
        String.parseHexDigit('a'),
        String.parseHexDigit('C'),
        String.parseHexDigit('N')
    );
  };

  func testType {
    def s: String;
    s = "Alusus";
    Console.print(s);
    Console.print("\nlength: %d\n", s.getLength());

    def i: Int;
    for i = 0, i < s.getLength(), ++i Console.print("s(%d) = %c\n", i, s(i));

    Console.print(s + " Language\n");

    s = "Hello {name}. Do you want to use {name} as your username?\n";
    Console.print(s.replace("{name}", "Yusuf"));

    s = "  \n\t\r hello world \n\r ";
    Console.print("trim:");
    Console.print(s.trim());
    Console.print("\ntrimStart:");
    Console.print(s.trimStart());
    Console.print("\ntrimEnd:");
    Console.print(s.trimEnd());
    Console.print("\nslice:");
    Console.print(s.slice(5, 6));
    Console.print("\n");

    if s.slice(12, 5) == "world" Console.print("Comparison successful!\n");

    def ss: Array[String] = s.trim().split(" ");
    for i = 0, i < ss.getLength(), ++i {
      Console.print(ss(i));
      Console.print("\n");
    };

    s = String.merge(ss, " - ");
    Console.print(s);
    Console.print("\n");

    s = "\t Latin Letters حروف غير لاتينية 非拉丁字母 ലാറ്റിൻ അല്ലാത്ത അക്ഷരങ്ങൾ गैर-लैटिन पत्र !@#$%^&*{}()";
    Console.print("No case change: %s\n", s.buf);
    Console.print("ToUpperCase: %s\n", s.toUpperCase().buf);
    Console.print("ToLowerCase: %s", s.toLowerCase().buf);
  };
};

Main.testStatics();
Main.testType();

