اشمل "مـتم/طـرفية"؛
اشمل "مـتم/نـص"؛
اشمل "الـقلب/بـيانات"؛

استخدم مـتم؛

عرف أمشتق_من: لقب الـقلب.أسـاسيات.أمشتق_من؛

دالة اطبع_بادئة (قياس: صحيح) {
    بينما قياس-- > 0 طـرفية.اطبع(" ")؛
}

دالة اطبع_شبم (كائن: سند[الـقلب.أسـاسيات.كـائن_بهوية]، بادئة: صحيح) {
    إذا كائن~مؤشر == 0 {
        طـرفية.اطبع("عدم\ج")؛
        ارجع؛
    }
    
    إذا أمشتق_من[كائن، الـقلب.أسـاسيات.نـص_بهوية] {
        طـرفية.اطبع("نـص_بهوية \"%s\"\ج"، مثل_سند[كائن، الـقلب.أسـاسيات.نـص_بهوية].القيمة.صوان)؛
        ارجع؛
    } وإلا إذا أمشتق_من[كائن، الـقلب.أسـاسيات.صـحيح_بهوية] {
        طـرفية.اطبع("صـحيح_بهوية %d\ج"، مثل_سند[كائن، الـقلب.أسـاسيات.صـحيح_بهوية].القيمة)؛
        ارجع؛
    } وإلا إذا أمشتق_من[كائن، الـقلب.أسـاسيات.طـبيعي_بهوية] {
        طـرفية.اطبع("طـبيعي_بهوية %d\ج"، مثل_سند[كائن، الـقلب.أسـاسيات.طـبيعي_بهوية].القيمة)؛
        ارجع؛
    } وإلا إذا أمشتق_من[كائن، الـقلب.أسـاسيات.عـائم_بهوية] {
        طـرفية.اطبع("عـائم_بهوية %f\ج"، مثل_سند[كائن، الـقلب.أسـاسيات.عـائم_بهوية].القيمة~مثل[عائم[64]])؛
        ارجع؛
    } وإلا إذا أمشتق_من[كائن، الـقلب.أسـاسيات.ثـنائي_بهوية] {
        إذا مثل_سند[كائن، الـقلب.أسـاسيات.ثـنائي_بهوية].القيمة {
            طـرفية.اطبع("ثـنائي_بهوية صح\ج")؛
        } وإلا {
            طـرفية.اطبع("ثـنائي_بهوية خطأ\ج")؛
        }
        ارجع؛
    } وإلا إذا أمشتق_من[كائن، الـقلب.بـيانات.شـبم.مـعرف] {
        طـرفية.اطبع("مـعرف %s\ج"، مثل_سند[كائن، الـقلب.بـيانات.شـبم.مـعرف].القيمة.القيمة.صوان)؛
        ارجع؛
    } وإلا إذا أمشتق_من[كائن، الـقلب.بـيانات.شـبم.نـص_حرفي] {
        طـرفية.اطبع("نـص_حرفي %s\ج"، مثل_سند[كائن، الـقلب.بـيانات.شـبم.نـص_حرفي].القيمة.القيمة.صوان)؛
        ارجع؛
    } وإلا إذا أمشتق_من[كائن، الـقلب.بـيانات.شـبم.صـحيح_حرفي] {
        طـرفية.اطبع("صـحيح_حرفي %s\ج"، مثل_سند[كائن، الـقلب.بـيانات.شـبم.صـحيح_حرفي].القيمة.القيمة.صوان)؛
        ارجع؛
    } وإلا إذا أمشتق_من[كائن، الـقلب.بـيانات.شـبم.مـحرف_حرفي] {
        طـرفية.اطبع("مـحرف_حرفي %s\ج"، مثل_سند[كائن، الـقلب.بـيانات.شـبم.مـحرف_حرفي].القيمة.القيمة.صوان)؛
        ارجع؛
    } وإلا إذا أمشتق_من[كائن، الـقلب.بـيانات.شـبم.عـائم_حرفي] {
        طـرفية.اطبع("عـائم_حرفي %s\ج"، مثل_سند[كائن، الـقلب.بـيانات.شـبم.عـائم_حرفي].القيمة.القيمة.صوان)؛
        ارجع؛
    }
    
    طـرفية.اطبع("%s.%s\ج"، كائن.هات_هويتي().مسار_الصنف.صوان، كائن.هات_هويتي().اسم_الصنف.صوان)؛
    
    عرف ع: طبيعي؛
    
    // طباعة الأعضاء
    عرف ربط: سند[الـقلب.بـيانات.ربـط](الـقلب.أسـاسيات.هات_وسيط[كائن، الـقلب.بـيانات.ربـط])؛
    إذا ربط~مؤشر != 0 {
        اطبع_بادئة(بادئة + 1)؛
        طـرفية.اطبع("الأعضاء:\ج")؛
        لكل ع = 0، ع < ربط.هات_عدد_الأعضاء()، ++ع {
            اطبع_بادئة(بادئة + 1)؛
            طـرفية.اطبع("-%s: "، ربط.هات_اسم_العضو(ع).صوان)؛
            اطبع_شبم(ربط.هات_عضوا(ع)، بادئة + 2)؛
        }
    }
    
    // طباعة عناصر مسماة
    عرف احتواء_تطبيقي: سند[الـقلب.بـيانات.احـتواء_تطبيقي](الـقلب.أسـاسيات.هات_وسيط[كائن، الـقلب.بـيانات.احـتواء_تطبيقي])؛
    إذا احتواء_تطبيقي~مؤشر != 0 {
        اطبع_بادئة(بادئة + 1)؛
        طـرفية.اطبع("عناصر مسماة:\ج")؛
        لكل ع = 0، ع < احتواء_تطبيقي.هات_عدد_العناصر()، ++ع {
            اطبع_بادئة(بادئة + 1)؛
            طـرفية.اطبع("-%s: "، احتواء_تطبيقي.هات_اسم_العنصر(ع).صوان)؛
            اطبع_شبم(احتواء_تطبيقي.هات_عنصرا(ع)، بادئة + 2)؛
        }
    }
    
    // طباعة عناصر
    عرف احتواء: سند[الـقلب.بـيانات.احـتواء](الـقلب.أسـاسيات.هات_وسيط[كائن، الـقلب.بـيانات.احـتواء])؛
    إذا احتواء~مؤشر != 0 && احتواء_تطبيقي~مؤشر == 0 {
        اطبع_بادئة(بادئة + 1)؛
        طـرفية.اطبع("عناصر:\ج")؛
        لكل ع = 0، ع < احتواء.هات_عدد_العناصر()، ++ع {
            اطبع_بادئة(بادئة + 1)؛
            طـرفية.اطبع("-")؛
            اطبع_شبم(احتواء.هات_عنصرا(ع)، بادئة + 2)؛
        }
    }
}

عرف ش: سند[الـقلب.أسـاسيات.كـائن_بهوية](شبم ا، ب: (5، 6)، ت، @مشترك دالة د {})؛
اطبع_شبم(ش، 0)؛

