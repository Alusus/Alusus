import "Srl/Console.alusus";
import "Srl/File.alusus";

def Main: module
{
  def start: function () => Void
  {
    def filename: ptr[Char];
    filename = "/tmp/alususfileoperationtest.txt";

    testWritingText(filename);
    testReadingText(filename);
    testWritingBinary(filename);
    testReadingBinary(filename);
  };

  def testWritingText: function (filename: ptr[Char])=>Void
  {
    def file: ptr[Void];
    file = Srl.File.openFile(filename, "w");
    Srl.File.printToFile(file, "Num of days in a week: %d", 7);
    Srl.File.closeFile(file);
  };

  def testReadingText: function (filename: ptr[Char])=>Void
  {
    def file: ptr[Void];
    def buffer: array[Char, 100];
    file = Srl.File.openFile(filename, "r");
    Srl.File.scanFromFile(file, "%[^0-9]", buffer~ptr);
    Srl.Console.print(buffer(0)~ptr);
    def num: Int;
    Srl.File.scanFromFile(file, "%i", num~ptr);
    Srl.Console.print("%d\n", num);
    Srl.File.closeFile(file);
  };

  def testWritingBinary: function (filename: ptr[Char])=>Void
  {
    def file: ptr[Void];
    def arr: array[Int, 8];
    def i: Int;
    for i = 0, i < 8, ++i {
      arr(i) = i * i;
    };
    file = Srl.File.openFile(filename, "w");
    Srl.File.writeToFile(arr~ptr, arr(0)~size~cast[Int], 8, file);
    Srl.File.closeFile(file);
  };

  def testReadingBinary: function (filename: ptr[Char])=>Void
  {
    def file: ptr[Void];
    def newArr: array[Int, 8];
    file = Srl.File.openFile(filename, "r");
    Srl.File.readFromFile(newArr~ptr, newArr(0)~size~cast[Int], 8, file);
    Srl.File.closeFile(file);
    def i: Int;
    for i = 0, i < 8, ++i {
      Srl.Console.print("element %d: %d\n", i, newArr(i));
    }
  }
};

run Main.start;
