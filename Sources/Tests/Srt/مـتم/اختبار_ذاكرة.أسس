اشمل "مـتم/ذاكـرة"؛
اشمل "مـتم/طـرفية"؛
اشمل "مـتم/نـص"؛
اشمل "نـبم"؛
استخدم مـتم؛

عرف رئـيسي: وحدة
{
    عرف ابدأ: دالة ()
    {
        عرف صوان: مؤشر[صـحيح]؛
        عرف س: صـحيح؛

        // اختبر "احجز".
        طـرفية.اطبع("اختبار `احجز`:\ج")؛
        صوان = ذاكـرة.احجز(صـحيح~حجم * 10)~مثل[مؤشر[صـحيح]]؛
        لكل س = 0، س < 10، ++س صوان~مثل[مؤشر[مصفوفة[صـحيح]]]~محتوى(س) = س * 2؛
        لكل س = 0، س < 10، ++س {
            طـرفية.اطبع("العنصر %d: %d\ج"، س، صوان~مثل[مؤشر[مصفوفة[صـحيح]]]~محتوى(س))؛
        }؛

        // اختبر `أعد_الحجز`.
        طـرفية.اطبع("\جاختبار `أعد_الحجز:\ج")؛
        صوان = ذاكـرة.أعد_الحجز(صوان، صـحيح~حجم * 20)~مثل[مؤشر[صـحيح]]؛
        لكل س = 0، س < 20، ++س صوان~مثل[مؤشر[مصفوفة[صـحيح]]]~محتوى(س) = س * 2؛
        لكل س = 0، س < 20، ++س {
            طـرفية.اطبع("العنصر %d: %d\ج"، س، صوان~مثل[مؤشر[مصفوفة[صـحيح]]]~محتوى(س))؛
        }؛

        ذاكـرة.حرر(صوان)؛

        // اختبر `احجز_مرصوف`.
        طـرفية.اطبع("\جاختبار `احجز_مرصوف`:\ج")؛
        // سنحصر الاختبار في نظام لينكس ونحاكي النتيجة على الأنظمة الأخرى.
        تمهيد {
          إذا نـص.أمتطابق(الـعملية.النظام، "linux") {
            نـبم.مدير_شبم.احشر_شبم(
              شبم {
                صوان = ذاكـرة.احجز_مرصوف(صـحيح~حجم، صـحيح~حجم * 10)~مثل[مؤشر[صـحيح]]؛
                لكل س = 0، س < 10، ++س صوان~مثل[مؤشر[مصفوفة[صـحيح]]]~محتوى(س) = س * 2؛
                لكل س = 0، س < 10، ++س {
                    طـرفية.اطبع("العنصر %d: %d\ج"، س، صوان~مثل[مؤشر[مصفوفة[صـحيح]]]~محتوى(س))؛
                }؛
                ذاكـرة.حرر(صوان)؛
              }
            )؛
          } وإلا {
            نـبم.مدير_شبم.احشر_شبم(
              شبم {
                طـرفية.اطبع("العنصر 0: 0\ج")؛
                طـرفية.اطبع("العنصر 1: 2\ج")؛
                طـرفية.اطبع("العنصر 2: 4\ج")؛
                طـرفية.اطبع("العنصر 3: 6\ج")؛
                طـرفية.اطبع("العنصر 4: 8\ج")؛
                طـرفية.اطبع("العنصر 5: 10\ج")؛
                طـرفية.اطبع("العنصر 6: 12\ج")؛
                طـرفية.اطبع("العنصر 7: 14\ج")؛
                طـرفية.اطبع("العنصر 8: 16\ج")؛
                طـرفية.اطبع("العنصر 9: 18\ج")؛
              }
            )؛
          }
        }

        // اختبر `اضبط` و `انسخ` و `قارن`.
        طـرفية.اطبع("\جاختبار `اضبط` و `انسخ` و `قارن`:\ج")؛
        صوان = ذاكـرة.احجز(صـحيح~حجم * 10)~مثل[مؤشر[صـحيح]]؛
        لكل س = 0، س < 10، ++س صوان~مثل[مؤشر[مصفوفة[صـحيح]]]~محتوى(س) = س * 2؛
        عرف صوان2: مؤشر[صـحيح]؛
        صوان2 = ذاكـرة.احجز(صـحيح~حجم * 10)~مثل[مؤشر[صـحيح]]؛
        ذاكـرة.اضبط(صوان2، 0، صـحيح~حجم * 10)؛
        طـرفية.اطبع("مقارنة مصفوفة بأصفار: %d\ج"، ذاكـرة.قارن(صوان، صوان2، صـحيح~حجم * 10))؛
        طـرفية.اطبع("مقارنة أصفار بمصفوفة: %d\ج"، ذاكـرة.قارن(صوان2، صوان، صـحيح~حجم * 10))؛
        ذاكـرة.انسخ(صوان2، صوان، صـحيح~حجم * 10)؛
        طـرفية.اطبع("مقارنة مصفوفتين متطابقتين: %d\ج"، ذاكـرة.قارن(صوان، صوان2، صـحيح~حجم * 10))؛
        ذاكـرة.حرر(صوان)؛
        ذاكـرة.حرر(صوان2)؛
    }؛
}؛

رئـيسي.ابدأ()؛
