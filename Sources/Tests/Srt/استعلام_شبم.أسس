اشمل "مـتم/طـرفية"؛
اشمل "مـتم/مـصفوفة"؛
اشمل "مـتم/تـطبيق"؛
اشمل "مـتم/نـص"؛
اشمل "الـقلب/بـيانات"؛
اشمل "نـبم"؛
استخدم مـتم؛

دالة أنشئ_إضافة_أسماء_العناصر (اسم_المتغير: نـص، العناصر: مـصفوفة[سند[الـقلب.أسـاسيات.كـائن_بهوية]]) {
    عرف ع: صحيح؛
    لكل ع = 0، ع < العناصر.هات_الطول()، ++ع {
        عرف عقدة: سند[الـقلب.بـيانات.عـقدة](مثل_سند[العناصر(ع)، الـقلب.بـيانات.عـقدة])؛
        عرف ت: سند[الـقلب.بـيانات.شـبم.تـعريف](مثل_سند[عقدة.المالك، الـقلب.بـيانات.شـبم.تـعريف])؛
        عرف ربط: سند[الـقلب.بـيانات.ربـط](الـقلب.أسـاسيات.هات_وسيط[ت، الـقلب.بـيانات.ربـط])؛
        نـبم.مدير_شبم.احشر_شبم(
            شبم { أسماء.أضف(نـص("{{اسم}}")) }،
            تـطبيق[نـص، سند[الـقلب.أسـاسيات.كـائن_بهوية]]()
                .حدد(نـص("أسماء")، الـقلب.بـيانات.شـبم.مـعرف(اسم_المتغير))
                .حدد(نـص("اسم")، ربط.هات_عضوا("name"))
        )؛
    }
}

ماكرو أنجز_هات_أسماء_العناصر [الشرط، المالك] {
    عرف أسماء: مـصفوفة[نـص]؛
    تمهيد {
        عرف العناصر: مـصفوفة[سند[الـقلب.أسـاسيات.كـائن_بهوية]]؛
        العناصر = نـبم.مدير_شبم.جد_عناصر(
            شبم { الشرط }،
            المالك،
            نـبم.خـيارات_الساعي._تخطى_المالكين_ | نـبم.خـيارات_الساعي._تخطى_الاستخدامات_ | نـبم.خـيارات_الساعي._تخطى_الأبناء_
        )؛
        أنشئ_إضافة_أسماء_العناصر(نـص("أسماء")، العناصر)؛
    }
    أرجع أسماء
}

دالة اطبع_مبدلات (عنصر: سند[الـقلب.أسـاسيات.كـائن_بهوية]) {
    عرف مبدلات: سند[الـقلب.بـيانات.احـتواء](نـبم.مدير_شبم.هات_المبدلات(عنصر))؛
    عرف ع: صحيح؛
    لكل ع = 0، ع < مبدلات.هات_عدد_العناصر()~مثل[صحيح]، ++ع {
        طـرفية.اطبع("%s\ج"، نـبم.مدير_شبم.هات_كلمة_المبدل_المفتاحية(مبدلات.هات_عنصرا(ع)).صوان)؛
    }
}

دالة اطبع_معطيات_مبدل (مبدل: سند[الـقلب.أسـاسيات.كـائن_بهوية]) {
    عرف معطيات: مـصفوفة[نـص]؛
    نـبم.مدير_شبم.هات_المعطيات_النصية_لمبدل(مبدل، معطيات)؛
    عرف ع: صحيح؛
    لكل ع = 0، ع < معطيات.هات_الطول()، ++ع {
        طـرفية.اطبع("%s\ج"، معطيات(ع).صوان)؛
    }
}

دالة اطبع_معطيات_مبدل_عنصر (عنصر: سند[الـقلب.أسـاسيات.كـائن_بهوية]، اسم_المبدل: مؤشر[مصفوفة[محرف]]) {
    عرف مبدلات: سند[الـقلب.بـيانات.احـتواء](نـبم.مدير_شبم.هات_المبدلات(عنصر))؛
    عرف مبدل: سند[الـقلب.أسـاسيات.كـائن_بهوية](نـبم.مدير_شبم.جد_مبدلا(مبدلات، اسم_المبدل))؛
    اطبع_معطيات_مبدل(مبدل)؛
}

دالة اطبع_معطيات_مبدل_عنصر2 (عنصر: سند[الـقلب.أسـاسيات.كـائن_بهوية]، اسم_المبدل: مؤشر[مصفوفة[محرف]]) {
    عرف مبدل: سند[الـقلب.أسـاسيات.كـائن_بهوية](نـبم.مدير_شبم.جد_مبدلا_لعنصر(عنصر، اسم_المبدل))؛
    اطبع_معطيات_مبدل(مبدل)؛
}

صنف صـنف {
    @مبدل_ا @مبدل_ب["ا-ب-ت"، "ج-ح-خ"] @مبدل_ت["د-ذ-ر"، "ز-س-ش"]
    عرف س: صحيح؛

    @خاص عرف ص: صحيح؛

    دالة هات_أسماء_المتغيرات (): مـصفوفة[نـص] {
        أنجز_هات_أسماء_العناصر[صنف_الكائن == "متغير"، هذا_الصنف~شبم]؛
    }

    @خاص دالة هات_أسماء_الدالات (): مـصفوفة[نـص] {
        أنجز_هات_أسماء_العناصر[صنف_الكائن == "دالة"، هذا_الصنف~شبم]؛
    }

    دالة هات_أسماء_العناصر_الخاصة (): مـصفوفة[نـص] {
        أنجز_هات_أسماء_العناصر[مبدل == "خاص"، هذا_الصنف~شبم]؛
    }
}

دالة هات_أسماء_الأصناف (): مـصفوفة[نـص] {
    أنجز_هات_أسماء_العناصر[صنف_الكائن == "صنف"، الـجذر~شبم]؛
}

دالة هات_أسماء_الوحدات (): مـصفوفة[نـص] {
    أنجز_هات_أسماء_العناصر[صنف_الكائن == "وحدة"، الـجذر~شبم]؛
}

دالة اطبع_أسماء (أسماء: مـصفوفة[نـص]) {
    عرف ع: صحيح؛
    لكل ع = 0، ع < أسماء.هات_الطول()، ++ع {
        طـرفية.اطبع("%s\ج"، أسماء(ع).صوان)؛
    }
}

عرف ص: صـنف؛
طـرفية.اطبع("أسماء المتغيرات:\ج")؛
اطبع_أسماء(ص.هات_أسماء_المتغيرات())؛

طـرفية.اطبع("\جأسماء الدالات:\ج")؛
اطبع_أسماء(ص.هات_أسماء_الدالات())؛

طـرفية.اطبع("\جأسماء العناصر الخاصة:\ج")؛
اطبع_أسماء(ص.هات_أسماء_العناصر_الخاصة())؛

طـرفية.اطبع("\جأسماء الأصناف:\ج")؛
اطبع_أسماء(هات_أسماء_الأصناف())؛

طـرفية.اطبع("\جأسماء الوحدات:\ج")؛
اطبع_أسماء(هات_أسماء_الوحدات())؛

طـرفية.اطبع("\جمبدلات صـنف.س:\ج")؛
اطبع_مبدلات(صـنف.س~شبم)؛

طـرفية.اطبع("\جمعطيات @مبدل_ب للعنصر صـنف.س:\ج")؛
اطبع_معطيات_مبدل_عنصر(صـنف.س~شبم، "مبدل_ب")؛

طـرفية.اطبع("\جمعطيات @مبدل_ت للعنصر صـنف.س:\ج")؛
اطبع_معطيات_مبدل_عنصر2(صـنف.س~شبم، "مبدل_ت")؛

