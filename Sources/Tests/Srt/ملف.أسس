اشمل "مـتم/طـرفية.أسس"؛
اشمل "مـتم/مـلف.أسس"؛

عرف رئـيسي: حزمة
{
  عرف إبدأ: دالة ()
  {
    عرف اسم_الملف: مؤشر[مـحرف]؛
    اسم_الملف = "/tmp/alususfileoperationtest.txt"؛

    اختبر_خزن_نص(اسم_الملف)؛
    اختبر_جلب_نص(اسم_الملف)؛
    اختبر_خزن_رقميات(اسم_الملف)؛
    اختبر_جلب_رقميات(اسم_الملف)؛
  }؛

  عرف اختبر_خزن_نص: دالة (اسم_الملف: مؤشر[مـحرف])
  {
    عرف ملف: مؤشر[فـراغ]؛
    ملف = مـتم.مـلف.افتح(اسم_الملف، "w")؛
    مـتم.مـلف.اطبع(ملف، "عدد أيام الأسبوع: %d"، 7)؛
    مـتم.مـلف.اغلق(ملف)؛
  }؛

  عرف اختبر_جلب_نص: دالة (اسم_الملف: مؤشر[مـحرف])
  {
    عرف ملف: مؤشر[فـراغ]؛
    ملف = مـتم.مـلف.افتح(اسم_الملف، "r")؛
    عرف صوان: مصفوفة[مـحرف، 100]؛
    مـتم.مـلف.تفحص(ملف، "%[^0-9]"، صوان~مؤشر)؛
    مـتم.طـرفية.اطبع(صوان(0)~مؤشر)؛
    عرف رقم: صـحيح؛
    مـتم.مـلف.تفحص(ملف، "%i"، رقم~مؤشر)؛
    مـتم.طـرفية.اطبع("%d\ج"، رقم)؛
    مـتم.مـلف.اغلق(ملف)
  }؛

  عرف اختبر_خزن_رقميات: دالة (اسم_الملف: مؤشر[مـحرف])
  {
    عرف م: مصفوفة[صـحيح، 8]؛
    عرف ع: صـحيح؛
    لكل ع = 0، ع < 8، ++ع م(ع) = ع * ع؛
    عرف ملف: مؤشر[فـراغ]؛
    ملف = مـتم.مـلف.افتح(اسم_الملف، "w")؛
    مـتم.مـلف.اكتب(م~مؤشر، م(0)~حجم~مثل[صـحيح]، 8، ملف)؛
    مـتم.مـلف.اغلق(ملف)؛
  }؛

  عرف اختبر_جلب_رقميات: دالة (اسم_الملف: مؤشر[مـحرف])
  {
    عرف م: مصفوفة[صـحيح، 8]؛
    عرف ملف: مؤشر[فـراغ]؛
    ملف = مـتم.مـلف.افتح(اسم_الملف، "r")؛
    مـتم.مـلف.اقرأ(م~مؤشر، م(0)~حجم~مثل[صـحيح]، 8، ملف)؛
    مـتم.مـلف.اغلق(ملف)؛
    عرف ع: صـحيح؛
    لكل ع = 0، ع < 8، ++ع مـتم.طـرفية.اطبع("العنصر %d: %d\ج"، ع، م(ع))؛
  }؛
}؛

نفذ رئـيسي.إبدأ؛

