اشمل "ـمتم/ـطرفية.أسس"؛
اشمل "ـمتم/ـملف.أسس"؛

عرف ـرئيسي: حزمة
{
  عرف إبدأ: دالة ()
  {
    عرف اسم_الملف: مؤشر[ـمحرف]؛
    اسم_الملف = "/tmp/alususfileoperationtest.txt"؛

    اختبر_خزن_نص(اسم_الملف)؛
    اختبر_جلب_نص(اسم_الملف)؛
    اختبر_خزن_رقميات(اسم_الملف)؛
    اختبر_جلب_رقميات(اسم_الملف)؛
  }؛

  عرف اختبر_خزن_نص: دالة (اسم_الملف: مؤشر[ـمحرف])
  {
    عرف ملف: مؤشر[ـفراغ]؛
    ملف = ـمتم.ـملف.افتح(اسم_الملف، "w")؛
    ـمتم.ـملف.اطبع(ملف، "عدد أيام الأسبوع: %d"، 7)؛
    ـمتم.ـملف.اغلق(ملف)؛
  }؛

  عرف اختبر_جلب_نص: دالة (اسم_الملف: مؤشر[ـمحرف])
  {
    عرف ملف: مؤشر[ـفراغ]؛
    ملف = ـمتم.ـملف.افتح(اسم_الملف، "r")؛
    عرف صوان: مصفوفة[ـمحرف، 100]؛
    ـمتم.ـملف.تفحص(ملف، "%[^0-9]"، صوان~مؤشر)؛
    ـمتم.ـطرفية.اطبع(صوان(0)~مؤشر)؛
    عرف رقم: ـصحيح؛
    ـمتم.ـملف.تفحص(ملف، "%i"، رقم~مؤشر)؛
    ـمتم.ـطرفية.اطبع("%d\ج"، رقم)؛
    ـمتم.ـملف.اغلق(ملف)
  }؛

  عرف اختبر_خزن_رقميات: دالة (اسم_الملف: مؤشر[ـمحرف])
  {
    عرف م: مصفوفة[ـصحيح، 8]؛
    عرف ع: ـصحيح؛
    لكل ع = 0، ع < 8، ++ع م(ع) = ع * ع؛
    عرف ملف: مؤشر[ـفراغ]؛
    ملف = ـمتم.ـملف.افتح(اسم_الملف، "w")؛
    ـمتم.ـملف.اكتب(م~مؤشر، م(0)~حجم~مثل[ـصحيح]، 8، ملف)؛
    ـمتم.ـملف.اغلق(ملف)؛
  }؛

  عرف اختبر_جلب_رقميات: دالة (اسم_الملف: مؤشر[ـمحرف])
  {
    عرف م: مصفوفة[ـصحيح، 8]؛
    عرف ملف: مؤشر[ـفراغ]؛
    ملف = ـمتم.ـملف.افتح(اسم_الملف، "r")؛
    ـمتم.ـملف.اقرأ(م~مؤشر، م(0)~حجم~مثل[ـصحيح]، 8، ملف)؛
    ـمتم.ـملف.اغلق(ملف)؛
    عرف ع: ـصحيح؛
    لكل ع = 0، ع < 8، ++ع ـمتم.ـطرفية.اطبع("العنصر %d: %d\ج"، ع، م(ع))؛
  }؛
}؛

نفذ ـرئيسي.إبدأ؛

