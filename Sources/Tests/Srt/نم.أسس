اشمل "مـتم/طـرفية.أسس"؛
اشمل "مـتم/مـحارف.أسس"؛
اشمل "مـتم/نـم.أسس"؛

عرف رئـيسي: وحدة
{
  عرف ابدأ: دالة ()
  {
    عرف اسم_الملف: مؤشر[مصفوفة[مـحرف]]؛
    اسم_الملف = "/tmp/alususfileoperationtest.txt"؛

    اختبر_خزن_نص(اسم_الملف)؛
    اختبر_جلب_نص(اسم_الملف)؛
    اختبر_خزن_رقميات(اسم_الملف)؛
    اختبر_جلب_رقميات(اسم_الملف)؛
    اختبر_الانتقال(اسم_الملف)؛
    اختبر_انشاء_ملفات_كاملة(اسم_الملف)؛
    اختبر_فتح_مجلد("/tmp")؛
  }؛

  عرف اختبر_خزن_نص: دالة (اسم_الملف: مؤشر[مصفوفة[مـحرف]])
  {
    عرف ملف: مؤشر[مـتم.نـم.مـلف]؛
    ملف = مـتم.نـم.افتح_ملف(اسم_الملف، "w")؛
    مـتم.نـم.اطبع(ملف، "عدد أيام الأسبوع: %d"، 7)؛
    مـتم.نـم.اغلق_ملف(ملف)؛
  }؛

  عرف اختبر_جلب_نص: دالة (اسم_الملف: مؤشر[مصفوفة[مـحرف]])
  {
    عرف ملف: مؤشر[مـتم.نـم.مـلف]؛
    ملف = مـتم.نـم.افتح_ملف(اسم_الملف، "r")؛
    عرف صوان: مصفوفة[مـحرف، 100]؛
    مـتم.نـم.امسح(ملف، "%[^0-9]"، صوان~مؤشر)؛
    مـتم.طـرفية.اطبع(صوان(0)~مؤشر)؛
    عرف رقم: صـحيح؛
    مـتم.نـم.امسح(ملف، "%i"، رقم~مؤشر)؛
    مـتم.طـرفية.اطبع("%d\ج"، رقم)؛
    مـتم.نـم.اغلق_ملف(ملف)
  }؛

  عرف اختبر_خزن_رقميات: دالة (اسم_الملف: مؤشر[مصفوفة[مـحرف]])
  {
    عرف م: مصفوفة[صـحيح، 8]؛
    عرف ع: صـحيح؛
    لكل ع = 0، ع < 8، ++ع م(ع) = ع * ع؛
    عرف ملف: مؤشر[مـتم.نـم.مـلف]؛
    ملف = مـتم.نـم.افتح_ملف(اسم_الملف، "w")؛
    مـتم.نـم.اكتب(م~مؤشر، م(0)~حجم~مثل[صـحيح]، 8، ملف)؛
    مـتم.نـم.اغلق_ملف(ملف)؛
  }؛

  عرف اختبر_جلب_رقميات: دالة (اسم_الملف: مؤشر[مصفوفة[مـحرف]])
  {
    عرف م: مصفوفة[صـحيح، 8]؛
    عرف ملف: مؤشر[مـتم.نـم.مـلف]؛
    ملف = مـتم.نـم.افتح_ملف(اسم_الملف، "r")؛
    مـتم.نـم.اقرأ(م~مؤشر، م(0)~حجم~مثل[صـحيح]، 8، ملف)؛
    مـتم.نـم.اغلق_ملف(ملف)؛
    عرف ع: صـحيح؛
    لكل ع = 0، ع < 8، ++ع مـتم.طـرفية.اطبع("العنصر %d: %d\ج"، ع، م(ع))؛
  }؛

  عرف اختبر_الانتقال: دالة (اسم_الملف: مؤشر[مصفوفة[مـحرف]])
  {
    عرف ملف: مؤشر[مـتم.نـم.مـلف]؛
    ملف = مـتم.نـم.افتح_ملف(اسم_الملف, "r");
    مـتم.نـم.انتقل(ملف, 10, مـتم.نـم.انـتقال._بداية_);
    عرف موقع: صـحيح;
    موقع = مـتم.نـم.هات_الموقع(ملف);
    مـتم.طـرفية.اطبع("%d\ج", موقع);
    مـتم.نـم.انتقل(ملف, 10, مـتم.نـم.انـتقال._نهاية_);
    موقع = مـتم.نـم.هات_الموقع(ملف);
    مـتم.طـرفية.اطبع("%d\ج", موقع);
    مـتم.نـم.اغلق_ملف(ملف);
  };

  عرف اختبر_انشاء_ملفات_كاملة: دالة (اسم_الملف: مؤشر[مصفوفة[مـحرف]])
  {
    عرف محتوى: مؤشر[مصفوفة[مـحرف]];
    محتوى = "كتابة ملف كامل بخطوة واحدة.\ج";
    مـتم.نـم.انشئ_ملف(اسم_الملف, محتوى, مـتم.مـحارف.جد_طول(محتوى));
    عرف صوان: مؤشر[مصفوفة[مـحرف]];
    عرف حجم: صـحيح;
    صوان = مـتم.نـم.اقرأ_ملف(اسم_الملف, حجم~مؤشر);
    مـتم.طـرفية.اطبع("%s%d\n", صوان, حجم);
  };

  عرف اختبر_فتح_مجلد: دالة (اسم_المجلد: مؤشر[مصفوفة[مـحرف]])
  {
    عرف مجلد: مؤشر[مـتم.نـم.مـجلد];
    مجلد = مـتم.نـم.افتح_مجلد(اسم_المجلد);
    مـتم.نـم.اغلق_مجلد(مجلد);
  };
}؛

رئـيسي.ابدأ()؛
