اشمل "مـتم/طـرفية.أسس"؛
اشمل "مـتم/مـحرفية.أسس"؛
اشمل "مـتم/مـصفوفة.أسس"؛

عرف رئـيسي: وحدة
{
  دالة اختبر_المشتركة {
    // بحث
    مـتم.طـرفية.اطبع("جد '-': %s\ج"، مـتم.مـحرفية.جد("123 - 456 - 789"، '-'))؛
    مـتم.طـرفية.اطبع("جد '-'، 4: %d\ج"، مـتم.مـحرفية.جد("123 - 456 - 789"، '-'، 4)~مثل[صـحيح[64]] != 0)؛
    مـتم.طـرفية.اطبع("جد '-'، 5: %d\ج"، مـتم.مـحرفية.جد("123 - 456 - 789"، '-'، 5)~مثل[صـحيح[64]] != 0)؛
    مـتم.طـرفية.اطبع("جد_آخر '-': %s\ج"، مـتم.مـحرفية.جد_آخر("123 - 456 - 789"، '-'))؛
    مـتم.طـرفية.اطبع("جد \"124\": %s\ج"، مـتم.مـحرفية.جد("123 - 124 - 125"، "124"))؛

    // مقارنة
    مـتم.طـرفية.اطبع("قارن \"123\" مع \"123\": %d\ج"، مـتم.مـحرفية.قارن("123"، "123"))؛
    مـتم.طـرفية.اطبع("قارن \"123\" مع \"1234\": %d\ج"، مـتم.مـحرفية.قارن("123"، "1234"))؛
    مـتم.طـرفية.اطبع("قارن 3 \"123\" مع \"1234\": %d\ج"، مـتم.مـحرفية.قارن("123"، "1234"، 3))؛

    // نسخ
    عرف صوان: مصفوفة[مـحرف، 100]؛
    مـتم.مـحرفية.انسخ(صوان~مؤشر، "-- بسم الله")؛
    مـتم.طـرفية.اطبع("انسخ: %s\ج"، صوان~مؤشر)؛
    مـتم.مـحرفية.انسخ(صوان~مؤشر، "++ لا حول ولا قوة إلا بالله"، 2)؛
    مـتم.طـرفية.اطبع("انسخ 2: %s\ج"، صوان~مؤشر)؛

    // سَلسلة
    مـتم.مـحرفية.سلسل(صوان~مؤشر، "، الحمد لله")؛
    مـتم.طـرفية.اطبع("سلسل: %s\ج"، صوان~مؤشر)؛
    مـتم.مـحرفية.سلسل(صوان~مؤشر، "!!!!!"، 2)؛
    مـتم.طـرفية.اطبع("سلسل 2: %s\ج"، صوان~مؤشر)؛

    // طول
    مـتم.طـرفية.اطبع("جد_الطول: %d\ج"، مـتم.مـحرفية.جد_الطول(صوان~مؤشر))؛
  }؛

  دالة اختبر_الصنف {
    عرف م: مـتم.مـحرفية؛
    م = "لغة"؛
    مـتم.طـرفية.اطبع(م)؛
    مـتم.طـرفية.اطبع("\جالطول: %d\ج"، م.جد_الطول())؛

    عرف ع: صحيح؛
    لكل ع = 0، ع < م.جد_الطول()، ++ع مـتم.طـرفية.اطبع("م(%d) = %c\ج"، ع، م(ع))؛

    مـتم.طـرفية.اطبع(م + " الأسس\ج")؛

    م = "مرحبا {اسم}. هل تود استخدام {اسم} كاسم المستخدم؟\ج"؛
    مـتم.طـرفية.اطبع(م.استبدل("{اسم}"، "يوسف"))؛

    م = "  \ج بسم الله \ج "؛
    مـتم.طـرفية.اطبع("شذب:")؛
    مـتم.طـرفية.اطبع(م.شذب())؛
    مـتم.طـرفية.اطبع("\جشذب_البداية:")؛
    مـتم.طـرفية.اطبع(م.شذب_البداية())؛
    مـتم.طـرفية.اطبع("\جشذب_النهاية:")؛
    مـتم.طـرفية.اطبع(م.شذب_النهاية())؛
    مـتم.طـرفية.اطبع("\جاجتزء:")؛
    مـتم.طـرفية.اطبع(م.اجتزء(4، 6))؛
    مـتم.طـرفية.اطبع("\ج")؛

    إذا م.اجتزء(11، 8) == "الله" مـتم.طـرفية.اطبع("نجحت المقارنة\ج")؛

    عرف مم: مـتم.مـصفوفة[مـتم.مـحرفية] = م.شذب().قطع(" ")؛
    لكل ع = 0، ع < مم.هات_الطول()، ++ع {
      مـتم.طـرفية.اطبع(مم(ع))؛
      مـتم.طـرفية.اطبع("\ج")؛
    }؛

    م = مـتم.مـحرفية.ادمج(مم، "_")؛
    مـتم.طـرفية.اطبع(م)؛
    مـتم.طـرفية.اطبع("\ج")؛

    عرف ترحيب: مـتم.مـحرفية = "Hello World - مع نص عربي"؛
    مـتم.طـرفية.اطبع("الى_اكبر: %s\n", ترحيب.كبر().buf)؛
    مـتم.طـرفية.اطبع("الى_اصغر: %s", ترحيب.صغر().buf)؛
  }؛
}؛

رئـيسي.اختبر_المشتركة()؛
رئـيسي.اختبر_الصنف()؛

