اشمل "مـتم/طـرفية"؛
اشمل "مـتم/سندات"؛
اشمل "مـتم/نـظام"؛
اشمل "الـقلب/بـيانات"؛
اشمل "مـتم/نـص"؛
اشمل "مـتم/تـطبيق"؛
اشمل "نـبم"؛

استخدم مـتم؛
استخدم الـقلب.أسـاسيات؛
استخدم الـقلب.بـيانات؛
استخدم الـقلب.بـيانات.شـبم؛
عرف سـندنا_لكائن_بهوية: لقب سـندنا[الـقلب.أسـاسيات.كـائن_بهوية]؛

دالة أنشئ_حيثما (كائن: سند[كـائن_بهوية]): سـندنا_لكائن_بهوية {
    عرف نتيجة: سـندنا_لكائن_بهوية؛
    إذا كائن~مؤشر == 0 {
        نـظام.فشل(1، "أنشئ_حيثما: الكائن معدوم.\ج")؛
    }
    
    إذا أمشتق_من[كائن، مـؤثر_مقارنة] {
        عرف ربط: سند[ربـط](هات_وسيط[كائن، ربـط])؛
        عرف حاوية_تطبيق: سند[احـتواء_تطبيقي](هات_وسيط[كائن، احـتواء_تطبيقي])؛
        إذا !نـبم.مدير_شبم.أنشئ_شبم(
            شبم { نـص("{{اسم}}") + نـص(" {{مؤثر}} '") + القيمة + نـص("'") }،
            تـطبيق[نـص، مؤشر]()
                .حدد(نـص("اسم")، حاوية_تطبيق.هات_عنصرا("first")~مؤشر)
                .حدد(نـص("مؤثر")، ربط.هات_عضوا("type")~مؤشر)
                .حدد(نـص("القيمة")، حاوية_تطبيق.هات_عنصرا("second")~مؤشر)،
            نتيجة
        ) {
            نـظام.فشل(1، "أنشئ_حيثما/مـؤثر_مقارنة: خطأ\ج")؛
        }
    } وإلا إذا أمشتق_من[كائن، مـؤثر_منطقي] {
        عرف ربط: سند[ربـط](هات_وسيط[كائن، ربـط])؛
        عرف حاوية_تطبيق: سند[احـتواء_تطبيقي](هات_وسيط[كائن، احـتواء_تطبيقي])؛
        عرف مؤثر: نـص_بهوية؛
        إذا مثل_سند[ربط.هات_عضوا("type")، نـص_بهوية].القيمة == "and" مؤثر = "و" وإلا مؤثر = "أو"؛
        إذا !نـبم.مدير_شبم.أنشئ_شبم(
            شبم { (شرط1) + نـص(" {{مؤثر}} ") + (شرط2) }،
            تـطبيق[نـص، مؤشر]()
                .حدد(نـص("شرط1")، أنشئ_حيثما(حاوية_تطبيق.هات_عنصرا("first")).كائن~مؤشر)
                .حدد(نـص("مؤثر")، مؤثر~مؤشر)
                .حدد(نـص("شرط2")، أنشئ_حيثما(حاوية_تطبيق.هات_عنصرا("second")).كائن~مؤشر)،
            نتيجة
        ) {
            نـظام.فشل(1، "أنشئ_حيثما/مـؤثر_منطقي: خطأ\ج")؛
        }
    } وإلا إذا أمشتق_من[كائن، قـوس] {
        عرف حاوية_تطبيق: سند[احـتواء_تطبيقي](هات_وسيط[كائن، احـتواء_تطبيقي])؛
        إذا !نـبم.مدير_شبم.أنشئ_شبم(
            شبم { نـص("(") + (شرط) + نـص(")") }،
            تـطبيق[نـص، مؤشر]()
                .حدد(نـص("شرط")، أنشئ_حيثما(حاوية_تطبيق.هات_عنصرا("operand")).كائن~مؤشر)،
            نتيجة
        ) {
            نـظام.فشل(1، "أنشئ_حيثما/قـوس: خطأ\ج")؛
        }
    } وإلا {
        إذا !نـبم.مدير_شبم.أنشئ_شبم(
            كائن~مؤشر،
            تـطبيق[نـص، مؤشر]()،
            نتيجة
        ) {
            نـظام.فشل(1، "فشل بناء الشرط.\ج")؛
        }
    }
    أرجع نتيجة؛
}

ماكرو حيثما [شرط] {
    تقييم {
        إذا !نـبم.مدير_شبم.احشر_شبم(
            أنشئ_حيثما((شبم شرط)~مثل[مؤشر[كـائن_بهوية]]~محتوى).كائن~مؤشر
        ) {
            نـظام.فشل(1، "فشل حشر الشرط.\ج")؛
        }
    }
}

دالة اختبر {
    عرف الاسم_الأول: نـص("محمد")؛
    عرف الوظيفة: نـص("مهندس")؛
    عرف حيثما: نـص = حيثما[الاسم == الاسم_الأول و (الوظيفة == الوظيفة أو الوظيفة == "محامي")]؛
    طـرفية.اطبع("%s\ج"، حيثما.صوان)؛
}
اختبر()؛

